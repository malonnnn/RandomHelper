class BioPipebombLauncher : Weapon
{
	Default
	{
		+WEAPON.NOALERT
		Weapon.Kickback 100;
		Weapon.AmmoUse1 0;
		Weapon.AmmoGive 12;
		Weapon.AmmoType "CompanionAmmo";
		Weapon.UpSound "Weapons/BioReady";
	}
	States
	{
		Spawn:
			BPBL M 1;
			Loop;
		Ready:
			BPBL ABCD 5 A_WeaponReady;
			Loop;
		Deselect:
			BPBL A 1 A_Lower;
			Loop;
		Select:
			BPBL A 1 A_Raise;
			Loop;
		Fire:
			TNT1 A 0 A_GunFlash;
			TNT1 A 0
			{
				let p = players[PlayerNumber()].mo;
				if(p.InvSel){
					int price = int(p.InvSel.cameraheight);
					int balance = int(p.CountInv("CompanionAmmo"));
					if(Utilities.canAfford(balance, price))
					{
						A_StartSound("Weapons/BioFire");
						A_FireProjectile("BioProj");
					}
				}
			}
			BPBL E 7;
			BPBL F 8;
			BPBL G 7;
			BPBL H 8;
			Goto Ready;
		Flash:
			BPBL I 2 A_Light(2);
			BPBL J 2;
			BPBL K 2;
			BPBL L 2;
			Goto LightDone;
		AltFire:
			TNT1 A 0 A_GunFlash;
			TNT1 A 0 A_StartSound("Weapons/BioFire");
			BPBL E 7 A_FireProjectile("BioProjAlt");
			BPBL F 8;
			BPBL G 7;
			BPBL H 8;
			Goto Ready;
	}
}

class BioProj : Actor
{
	Default
	{
		+Shootable
		+DontSplash
		+SeekerMissile
		+ForceRadiusDmg
		+DontFall
		+ThruSpecies
		Radius 5;
		Height 5;
		RenderStyle "Translucent";
		Alpha .7;
		Decal "BioDecal";
		Speed 30;
		DamageType "Companion";
		Species "Companion";
		Projectile;
	}
	States
	{
		Spawn:
			BPBP ABC 3 Bright A_SpawnItemEx("BioPuff",0,0,0,0,0,0,0,128);
			Loop;
		ChangeTID:
			TNT1 A 0 A_SetShootable;
			TNT1 A 0 A_Stop;
			TNT1 A 0 Thing_ChangeTID(0,2648);
			GoTo Death;
		Stick:
			BPBP AAABBBCCC 1 Bright A_Fire(35);
			Loop;
		Death:
			TNT1 A 0 A_JumpIf(tid == 2648,1);
			Goto ChangeTID;
			TNT1 A 0
			{

				let p = players[PlayerNumber()].mo;
				int price = int(p.InvSel.cameraheight);
				int balance = int(p.CountInv("CompanionAmmo"));

				if(Utilities.canAfford(balance, price))
				{
					A_StartSound("Weapons/BioProj");
					let g = Globals(EventHandler.Find("Globals"));
					String className = p.InvSel.GetClassName();
					// className.StripRight("Item"); // its bugged, maybe fixed one day. "ArachnophyteItem".StripRight("Item") returns "Arachnophy"
					className = className.Left(className.Length() - 4);
					g.createCompanion(className, pos, p, price, p.InvSel.GetTag());

					// remove item from inventory since shot with gun and not pressed "activate item" button
					p.TakeInventory(p.InvSel.GetClassName(), 1);
					p.TakeInventory("CompanionAmmo", price);
				}
			}
			TNT1 A 0 A_SpawnItemEx("BioPuff",0,0,0,0,0,0,0,128);
			BPBP DEF 6 Bright;
			Stop;
	}
}

class BioProjAlt : Actor
{
	Default
	{
		+Shootable
		+DontSplash
		+SeekerMissile
		+ForceRadiusDmg
		+DontFall
		+ThruSpecies
		Radius 5;
		Height 5;
		RenderStyle "Translucent";
		Alpha .7;
		Decal "BioDecal";
		Speed 30;
		Species "Companion";
		DamageType "Companion";
		Projectile;
	}
	States
	{
		Spawn:
			BPBP NOP 3 Bright A_SpawnItemEx("BioPuff");
			Loop;
		ChangeTID:
			TNT1 A 0 A_SetShootable;
			TNT1 A 0 A_Stop;
			TNT1 A 0 Thing_ChangeTID(0,2648);
			GoTo Death;
		Stick:
			BPBP AAABBBCCC 1 Bright A_Fire(35);
			Loop;
		Death:
			TNT1 A 0 A_JumpIf(tid == 2648,1);
			Goto ChangeTID;
			TNT1 A 0
			{
				//let p = players[PlayerNumber()].mo;
				//int fx = pos.x + cos(p.angle);
				//int fy = pos.y + sin(p.angle);
				bool spawnedAtLeastOne = false;
      	let g = Globals(EventHandler.Find("Globals"));
				for(int i = 0; i < g.companions.Size(); i++)
				{
					spawnedAtLeastOne = true;
					g.companions[i].SetOrigin(pos, true);
				}
			}
			TNT1 A 0 A_SpawnItemEx("BioPuff");
			BPBP DEF 6 Bright;
			Stop;
	}
}

class BioPuff : Actor
{
	Default
	{
		+NoBlockMap
		+NoGravity
		+Randomize
		+PuffOnActors
		+ThruSpecies
		Species "Companion";
		DamageType "Companion";
		RenderStyle "Translucent";
		Radius 0;
		Height 0;
		Scale .5;
		Alpha .25;
		VSpeed 1;
		Mass 5;
	}
	States
	{
		Spawn:
			BPBP GHIJKLMIHG 2 Bright;
			Stop;
	}
}

class CompanionAmmo : Ammo
{
  Default
  {
    //+INVENTORY.HUBPOWER
    //+SPRITEANGLE
    //SpriteAngle 180;
    Inventory.Amount 2;
    Inventory.MaxAmount 999;
    Inventory.InterHubAmount 999;

		Ammo.BackpackAmount 2;
		Ammo.BackpackMaxAmount 999;
  }
  States
	{
    Spawn:
      BIOC A -1;
      Stop;
	}
}