class BaseDeployer : Weapon
{
	int price;
	property price : price;
	Default
	{
		+WEAPON.NOALERT
		+SPRITEANGLE
    SpriteAngle 180;
		Weapon.Kickback 100;
		Weapon.AmmoGive1 2;
		Weapon.AmmoGive2 1;
		Weapon.AmmoUse1 0;
		Weapon.UpSound "Weapons/BioReady";
		Weapon.AmmoType1 "CompanionAmmo";
		Inventory.Amount 1;
		Weapon.SlotNumber 1;
		Scale 0.5;
	}
	States
	{
		Spawn:
			BPBL M 1;
			Loop;
		Ready:
			BPBL ABCD 5 A_WeaponReady;
			Loop;
		Deselect:
			BPBL A 1 A_Lower;
			Loop;
		Select:
			BPBL A 1 A_Raise;
			Loop;
		Fire:
			TNT1 A 0 A_GunFlash;
			TNT1 A 0
			{
				Actor p = players[PlayerNumber()].mo;
				int balance = int(p.CountInv("CompanionAmmo"));
				int numCompanions = p.CountInv(invoker.ammotype2);
				String tag = invoker.getTag();
				if(Utilities.canAfford(balance, invoker.price, numCompanions, tag))
				{
					A_StartSound("Weapons/BioFire");
					A_FireProjectile("BioProj");
				}
			}
			BPBL E 7;
			BPBL F 8;
			BPBL G 7;
			BPBL H 8;
			Goto Ready;
		AltFire:
			TNT1 A 0 A_GunFlash;
			TNT1 A 0 A_StartSound("Weapons/BioFire");
			BPBL E 7 A_FireProjectile("BioProjAlt");
			BPBL F 8;
			BPBL G 7;
			BPBL H 8;
			Goto Ready;
	}

	override bool ShouldStay ()
	{
		return false;
	}
}

class BioProj : Actor
{
	Default
	{
		+Shootable
		+DontSplash
		+SeekerMissile
		+ForceRadiusDmg
		+DontFall
		+ThruSpecies
		Radius 5;
		Height 5;
		RenderStyle "Translucent";
		Alpha .7;
		Decal "BioDecal";
		Speed 30;
		DamageType "Companion";
		Species "Companion";
		Projectile;
	}
	States
	{
		Spawn:
			BPBP ABC 3 Bright A_SpawnItemEx("BioPuff");
			Loop;
		Stick:
			BPBP AAABBBCCC 1 Bright A_Fire(35);
			Loop;
		Death:
			TNT1 A 0
			{				
				BaseDeployer gun = BaseDeployer(target.player.readyweapon);
				if(gun){
					String tag = gun.getTag();
					String ammotype = gun.AmmoType2.getclassname();
					
					int price = gun.price;		
					int balance = int(target.CountInv("CompanionAmmo"));
					int numCompanions = target.CountInv(ammotype);


					if(Utilities.canAfford(balance, price, numCompanions, tag))
					{
						A_StartSound("Weapons/BioProj");
						
						// className.StripRight("Item"); // its bugged, maybe fixed one day. "ArachnophyteItem".StripRight("Item") returns "Arachnophy"
						
						String className = ammotype.Left(ammotype.Length() - 4);
						let g = Globals(EventHandler.Find("Globals"));
						g.createCompanion(className, pos, target, price, tag, false, numCompanions);
					}
				}
			}
			BPBP DEF 6 Bright;
			Stop;
	}
}

class BioProjAlt : Actor
{
	Default
	{
		+Shootable
		+DontSplash
		+SeekerMissile
		+ForceRadiusDmg
		+DontFall
		+ThruSpecies
		Radius 5;
		Height 5;
		RenderStyle "Translucent";
		Alpha .7;
		Decal "BioDecal";
		Speed 30;
		Species "Companion";
		DamageType "Companion";
		Projectile;
	}
	States
	{
		Spawn:
			BPBP NOP 3 Bright A_SpawnItemEx("BioPuff");
			Loop;
		ChangeTID:
			TNT1 A 0 A_SetShootable;
			TNT1 A 0 A_Stop;
			TNT1 A 0 Thing_ChangeTID(0,2648);
			GoTo Death;
		Stick:
			BPBP AAABBBCCC 1 Bright A_Fire(35);
			Loop;
		Death:
			TNT1 A 0 A_JumpIf(tid == 2648,1);
			Goto ChangeTID;
			TNT1 A 0
			{
				bool spawnedAtLeastOne = false;
      	let g = Globals(EventHandler.Find("Globals"));
				for(int i = 0; i < g.companions.Size(); i++)
				{
					spawnedAtLeastOne = true;
					g.companions[i].SetOrigin(pos, true);
				}
			}
			BPBP DEF 6 Bright;
			Stop;
	}
}

class BioPuff : Actor
{
	Default
	{
		+NoBlockMap
		+NoGravity
		+Randomize
		+PuffOnActors
		+ThruSpecies
		Species "Companion";
		DamageType "Companion";
		RenderStyle "Translucent";
		Radius 0;
		Height 0;
		Scale .5;
		Alpha .25;
		VSpeed 1;
		Mass 5;
	}
	States
	{
		Spawn:
			BPBP GHIJKLMIHG 2 Bright;
			Stop;
	}
}

class CompanionAmmo : Ammo
{
  Default
  {
    Inventory.Amount 2;
    Inventory.MaxAmount 999;
    Inventory.InterHubAmount 999;

		Ammo.BackpackAmount 2;
		Ammo.BackpackMaxAmount 999;
  }
  States
	{
    Spawn:
      BIOC A -1;
      Stop;
	}
}