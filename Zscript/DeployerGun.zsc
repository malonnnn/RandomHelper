class BaseDeployer : Weapon
{
  int price;
  property price : price;
  int removeWeapon;
  property removeWeapon : removeWeapon;

  BioProj proj;
  BioProjAlt altproj;
  Default
  {
    +WEAPON.NOALERT
    +SPRITEANGLE
    +WEAPON.PRIMARY_USES_BOTH
    +WEAPON.ALT_AMMO_OPTIONAL
    +WEAPON.NO_AUTO_SWITCH
    SpriteAngle 180;
    Weapon.Kickback 100;
    Weapon.AmmoGive1 2;
    Weapon.AmmoGive2 1;
    Weapon.AmmoUse1 0;
    Weapon.AmmoUse2 0;
    Weapon.UpSound "Weapons/BioReady";
    Weapon.AmmoType1 "CompanionAmmo";
    Weapon.SlotNumber 3;
    Scale 0.3;

    BaseDeployer.removeWeapon 0;
  }
  States
  {
    Spawn:
      BPBL M 1;
      Loop;
    Ready:
      BPBL ABCD 5 A_WeaponReady;
      Loop;
    Deselect:
      BPBL A 1 A_Lower;
      Loop;
    Select:
      BPBL A 1 A_Raise;
      Loop;
    Fire:
      TNT1 A 0
      {
        A_GunFlash();
        let p = self;
        int numCompanions = p.CountInv(invoker.ammotype2);
        int balance = p.CountInv(invoker.ammotype1);
        if(balance >= invoker.price && numCompanions >= 1)
        {
          String tag = invoker.getTag();

          A_StartSound("Weapons/BioFire");

          String gunClassName = invoker.GetClassName();
          invoker.proj = BioProj(A_FireProjectile("BioProj"));

          invoker.proj.companionClass = gunClassName.Left(gunClassName.Length() - 3);
          invoker.proj.niceName = tag;
          invoker.proj.numCompanions = numCompanions;
          invoker.proj.price = invoker.price;

          // if price is free, reimburse 1 gun ammo
          if(invoker.price <= 0)
          {
            A_GiveInventory(gunClassName, 1);
          }
        }
      }
    Hold:
      BPBL E 7;
      BPBL F 8;
      BPBL G 7;
      BPBL H 8 A_Refire("Hold");
      TNT1 A 0
      {
        if(invoker.proj){
          invoker.proj.A_Die();
        }
      }
      GoTo Ready;
    AltFire:
      TNT1 A 0
      {
        A_GunFlash();
        A_StartSound("Weapons/BioFire");
        invoker.altproj = BioProjAlt(A_FireProjectile("BioProjAlt"));
      }
    AltHold:
      BPBL E 7;
      BPBL F 8;
      BPBL G 7;
      BPBL H 8 A_Refire("AltHold");
      TNT1 A 0
      {
        if(invoker.altproj){
          invoker.altproj.A_Die();
        }
      }
      GoTo Ready;
  }

  override bool ShouldStay ()
  {
    return false;
  }

  override void Tick()
  {
    Super.Tick();
    if(self.removeWeapon != 0){
      if(self.removeWeapon > 70){
        owner.player.cheats |= CF_INSTANTWEAPSWITCH;
        self.Destroy();
      }else{
        self.removeWeapon++;
      }
    }
  }
}

class BioProj : Actor
{
  String companionClass;
  property companionClass : companionClass;
  String niceName;
  property niceName : niceName;
  int numCompanions;
  property numCompanions : numCompanions;
  int price;
  property price : price;
  Default
  {
    -NOGRAVITY
    +LOWGRAVITY
    +ROCKETTRAIL
    BounceType "Hexen";

    +Shootable
    +DontSplash
    +SeekerMissile
    +ForceRadiusDmg
    +DontFall
    +ThruSpecies
    Radius 5;
    Height 5;
    RenderStyle "Translucent";
    Alpha .7;
    Decal "BioDecal";
    Speed 5;
    DamageType "Companion";
    Species "Companion";
    Projectile;
  }
  States
  {
    Spawn:
      BPBP ABC 3 Bright A_SpawnItemEx("BioPuff");
    Flying:
      BPBP ABC 3 Bright;
      Loop;
    Death:
      TNT1 A 0
      {
        vel.x = 0;
        vel.y = 0;
        vel.z = 0;
        A_StartSound("Weapons/BioProj");
        let g = Globals(EventHandler.Find("Globals"));
        g.createCompanion(companionClass, pos, target, niceName, false);
        
        let p = PlayerPawn(target);
        let p2 = target;
        let gun = BaseDeployer(p.player.readyweapon);
        p2.TakeInventory(companionClass.."Ammo", 1);
        p2.TakeInventory("CompanionAmmo", price);
        if(numCompanions <= 1)
        {
          p.player.cheats &= ~CF_INSTANTWEAPSWITCH;
          p.player.pendingweapon = p.PickNextWeapon();
          gun.removeWeapon = 1;
        }
      }
      BPBP DEF 6 Bright;
      Stop;
  }
}

class BioProjAlt : Actor
{
  Default
  {
    -NOGRAVITY
    +LOWGRAVITY
    +ROCKETTRAIL
    BounceType "Hexen";

    +Shootable
    +DontSplash
    +SeekerMissile
    +ForceRadiusDmg
    +DontFall
    +ThruSpecies
    Radius 5;
    Height 5;
    RenderStyle "Translucent";
    Alpha .7;
    Decal "BioDecal";
    Speed 5;
    Species "Companion";
    DamageType "Companion";
    Projectile;
  }
  States
  {
    Spawn:
      BPBP NOP 3 Bright A_SpawnItemEx("BioPuff");
    Flying:
      BPBP NOP 3;
      Loop;
    Death:
      TNT1 A 0
      {
        vel.x = 0;
        vel.y = 0;
        vel.z = 0;
        bool spawnedAtLeastOne = false;
        let g = Globals(EventHandler.Find("Globals"));
        for(int i = 0; i < g.companions.Size(); i++)
        {
          spawnedAtLeastOne = true;
          g.companions[i].SetOrigin(pos, true);
        }
      }
      BPBP DEF 6 Bright;
      Stop;
  }
}

class BioPuff : Actor
{
  Default
  {
    +NoBlockMap
    +NoGravity
    +Randomize
    +PuffOnActors
    +ThruSpecies
    Species "Companion";
    DamageType "Companion";
    RenderStyle "Translucent";
    Radius 0;
    Height 0;
    Scale .5;
    Alpha .25;
    VSpeed 1;
    Mass 5;
  }
  States
  {
    Spawn:
      BPBP GHIJKLMIHG 2 Bright;
      Stop;
  }
}

class CompanionAmmo : Ammo
{
  Default
  {
    -NoGravity
    -Float
    -DontFall
    Inventory.Amount 2;
    Inventory.MaxAmount 999;
    Inventory.InterHubAmount 999;
    Inventory.PickupMessage "";
    Ammo.BackpackAmount 2;
    Ammo.BackpackMaxAmount 999;
  }
  States
  {
    Spawn:
      BIOC A -1;
      Stop;
  }
}