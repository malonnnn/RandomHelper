Class Cryocubus : Actor
{	
	Default
	{
		+FRIENDLY;
		Species "Companion";
		DamageFactor "Companion", 0;
		Health 600;
		Radius 48;
		Height 64;
		Mass 1000;
		Speed 8;
		PainChance 80;
		DamageFactor "Ice", 0;
		Monster;
		+FLOORCLIP;
		+BOSSDEATH;
		SeeSound "fatso/sight";
		PainSound "fatso/pain";
		DeathSound "fatso/death";
		ActiveSound "fatso/active";
		Obituary "%o was frozen by a Cryocubus.";
	}
	States
	{
	Spawn:
		HIEL AB 15 A_Look();
		Loop;
	See:
		HIEL AABBCCDDEEFF 4	A_Chase("none","none"); // don't just attack willy-nilly
	MissileCheck:
		TNT1 A 0
		{	// instead make sure that the target is alive, in sight and in range
			if (target && target.health > 0 && Checksight(target) && Distance3D(target) <= 450) SetStateLabel("Missile");
		}
		Goto See;
	Missile:
		TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
		TNT1 A 0 A_FatRaise();
		HIEL IIIIIII 5 A_FaceTarget(); // increased attack telegraph duration
	MissileLoop:
		TNT1 A 0
		{
			A_FaceTarget();	
			if (!target || target.health < 1 || !Checksight(target) || Distance3D(target) > 450)
				return ResolveState("MissileStop"); // stop the attack if the target is dead, out of sight or out of range
			A_AlertMonsters(0, AMF_TARGETEMITTER);
			A_StartSound("hielo/llama",0);
			return ResolveState(null);
		}
		HIEL HHHHH random(1,2)
		{
			A_SpawnProjectile("CryocubusLanzaHielo",28,-24,random(-5,5),CMF_AIMOFFSET|CMF_CHECKTARGETDEAD,random(-5,5));
			A_SpawnProjectile("CryocubusLanzaHielo",28,24,random(-5,5),CMF_AIMOFFSET|CMF_CHECKTARGETDEAD,random(-5,5));
		}
		Loop;
	MissileStop:
		HIEL G 20;
		Goto See;
	Pain:
		HIEL J 3;
		HIEL J 3 A_Pain();
		TNT1 A 0 A_Jump(127,"MissileCheck");
		Goto See;
	Death:
		HIEL K 6;
		HIEL L 6 A_Scream();
		HIEL M 6 A_NoBlocking();
		HIEL NOP 6;
		HIEL P -1 A_BossDeath();
		Stop;
	Raise:
		HIEL R 5;
		HIEL QPONMLK 5;
		Goto See;
	}
}

Class CryocubusLanzaHielo : Actor
{
	Default
	{
		Species "Companion";
		DamageType "Companion";
		Radius 10;
		Height 10;
		Speed 20;
		Alpha 0.5;
		Projectile;
		RenderStyle "Translucent";
		+FORCEXYBILLBOARD;
		+STRIFEDAMAGE;
		+BLOODLESSIMPACT;
		+FORCERADIUSDMG;
		+PAINLESS;	// Pain is handled in SpecialMissileHit to prevent stunlocking monsters
		Obituary "%o was frozen by a Cryocubus.";
	}
	States
	{
	Spawn:
		TNT1 A 2;
		HUMO AB 1 A_Explode (1,20,XF_NOSPLASH|XF_THRUSTLESS,false,10, 0, 0, "CompanionBulletPuff", "Companion");
		HUMO CDEFGHIJKLMN random(1,2)
		{
			A_Explode (1,40,XF_NOSPLASH|XF_THRUSTLESS,false,20, 0, 0, "CompanionBulletPuff", "Companion");
			scale.x += 0.03;
			scale.y += 0.03;
			alpha -= 0.03;
		}
		HUMO OP 1 A_Explode(1,40,XF_NOSPLASH|XF_THRUSTLESS,false,15, 0, 0, "CompanionBulletPuff", "Companion");
		HUMO QR 1 A_Explode(1,30,XF_NOSPLASH|XF_THRUSTLESS,false,10, 0, 0, "CompanionBulletPuff", "Companion");
		HUMO ST 1 A_Explode(1,20,XF_NOSPLASH|XF_THRUSTLESS,false,5, 0, 0, "CompanionBulletPuff", "Companion");
		Stop;
	}
	override int SpecialMissileHit(Actor victim) // handle slowdown and pain effects on the victim
	{
		if (victim && (!target || victim != target) && victim.bSHOOTABLE && victim.health > 0)
		{
			int df = victim.ApplyDamageFactor("Ice",1); // no slowdown if victim is resistant to Ice damage
			if (df)
			{
				victim.A_GiveInventory("CryocubusSlowdown"); // slow down victim
				if (victim.bISMONSTER && victim.tics > 0)
				{
					int rnd =  random(1,500);
					if (rnd > 490) victim.tics += 1; // chance to reduce anim speed
					else if (rnd == 1 && victim.bNOPAIN == false && victim.painchance > 0) // chance to force pain
					{
						state painstate = victim.Findstate("Pain");
						if (painstate) victim.SetState(painstate); 
					}
				}
			}
		}
		return 1;
	}
}

Class CryocubusSlowdown : PowerSpeed
{
	Default
	{
		Powerup.Duration 5;
		Speed 0.5;
	}
	override void InitEffect()
	{
		super.InitEffect();
		if (owner && owner.bISMONSTER) owner.speed = owner.default.speed*0.5;
	}
	override void EndEffect()
	{
		super.EndEffect();
		if (owner && owner.bISMONSTER) owner.speed = owner.default.speed;
	}
	
}