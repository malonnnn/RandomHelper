Class FormerRanger : Actor
{
  Default
  {
    MONSTER;
    +FRIENDLY
    Species "Companion";
    DamageFactor "Companion", 0;
    obituary "%o was hunted down by a Ranger.";
    health 50;
    radius 20;
    height 56;
    mass 100;
    speed 8;
    painchance 170;
    Decal "BulletChip";
    seesound "grunt/sight";
    attacksound "grunt/attack2";//change to "grunt/attack" for the stock sound
    painsound "grunt/pain";
    deathsound "grunt/death";
    activesound "grunt/active";
    dropitem "Clip", 255;
    dropitem "Clip", 128;
    MONSTER;
    +FLOORCLIP
  }
  states
  {
    Spawn:
      BPOS AB 10 A_Look;
      loop;
    See:
      BPOS AABBCCDD 3 A_Chase;
      loop;
    Missile:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      BPOS E 10 A_FaceTarget;
      goto WhichMissileType;
    WhichMissileType:
      BPOS E 0 A_JumpIfCloser(768, "BurstFire");
      BPOS E 0 A_Jump(64, "SnipeFire2");
      goto SnipeFire;
    SnipeFire:
      BPOS F 4 bright A_CustomBulletAttack(10, 1, 1, random(1,5) * 3, "CompanionBulletPuff", 0, CBAF_NORANDOM);
      BPOS E 10 A_CPosRefire;
      goto WhichMissileType;
    SnipeFire2:
      BPOS EE 10 A_FaceTarget;
      BPOS F 4 bright A_CustomBulletAttack(8, 1, 1, random(1,5) * 3, "CompanionBulletPuff", 0, CBAF_NORANDOM);
      BPOS E 10 A_CPosRefire;
      goto WhichMissileType;
    BurstFire:
      BPOS F 4 bright A_CustomBulletAttack(13, 2, 1, random(1,5) * 3, "CompanionBulletPuff", 0, CBAF_NORANDOM);//A_CPosAttack
      BPOS E 3 A_FaceTarget;
      BPOS F 4 bright A_CustomBulletAttack(13, 2, 1, random(1,5) * 3, "CompanionBulletPuff", 0, CBAF_NORANDOM);//A_CPosAttack
      BPOS E 3 A_FaceTarget;
      BPOS F 4 bright A_CustomBulletAttack(13, 2, 1, random(1,5) * 3, "CompanionBulletPuff", 0, CBAF_NORANDOM);//A_CPosAttack
      BPOS E 10 A_CPosRefire;
      goto WhichMissileType;
    Pain:
      BPOS G 3;
      BPOS G 3 A_Pain;
      goto See;
    Death:
      BPOS H 5;
      BPOS I 5 A_Scream;
      BPOS J 5 A_NoBlocking;
      BPOS K 5;
      BPOS L -1;
      stop;
    XDeath:
      BPOS M 5;
      BPOS N 5 A_XScream;
      BPOS O 5 A_NoBlocking;
      BPOS PQRST 5;
      BPOS U -1;
      stop;
    Raise:
      BPOS LKJIH 5;
      goto See;
  }
}