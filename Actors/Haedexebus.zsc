Class Haedexebus : CompanionFatso
{
  Default
  {
    MONSTER;
    +FRIENDLY
    Species "Companion";
    DamageFactor "Companion", 0;
    Radius 72;
    Height 88;
    Health 750;
    Mass 600;
    Speed 20;
    +FLOAT
    +FLOATBOB
    -FLOORCLIP
    +NOGRAVITY
    +MISSILEMORE
    +NOINFIGHTING
    SEESOUND "UNMNROAM";
    PAINSOUND "UNMNPAIN";
    DEATHSOUND "UNMNDYIN";
    Decal "RedPlasmaScorch";
    Obituary "%o was unmade by a Haedexebus.";
  }
  States
	{
    Spawn:
      UNMB AABB 8 A_Look;
      Loop;
    See:
      UNMB AAAABBBB 4 A_Chase;
      Loop;
    Missile:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      UNMB C 5 A_FatRaise;
      UNMB CD 5 A_FaceTarget;
      UNMB D 0 A_Jump(128,"Missile2");
      UNMB E 0 Bright A_StartSound("UNMBWOSH");
      UNMB E 0 BRIGHT LIGHT("UNMNBALLR") A_SpawnProjectile("FatMace", 48, 32, random(-7,4));
      UNMB E 0 Bright A_StartSound("UNMBWOSH");
      UNMB E 5 BRIGHT LIGHT("UNMNBALLL") A_SpawnProjectile("FatMace", 48, -32, random(-4,7));
      UNMB CD 5 A_FaceTarget;
      Goto See;
    Missile2:
      UNMB E 0 Bright A_StartSound("UNMNRAIL");
      UNMB E 5 BRIGHT LIGHT("UNMNRALR") A_CustomRailgun(random(10,20), 32, "black", "red", RGF_NOPIERCING, 1, 2, "CompanionBulletPuff", 0, 0, 1000, random(8,16), 8.0, 2.0, "FatRail", 0);
      UNMB E 5 BRIGHT LIGHT("UNMNRALL") A_CustomRailgun(random(10,20), -32, "red", "black", RGF_NOPIERCING, 1, 2, "CompanionBulletPuff", 0, 0, 1000, random(8,16), 8.0, 2.0, "FatRail", 0);
      UNMB CD 5 A_FaceTarget;
      UNMB E 0 Bright A_StartSound("UNMNRAIL");
      UNMB E 5 BRIGHT LIGHT("UNMNRALR") A_CustomRailgun(random(10,20), 32, "red", "black", RGF_NOPIERCING, 1, 2, "CompanionBulletPuff", 0, 0, 1000, random(8,16), 8.0, 2.0, "FatRail", 0);
      UNMB E 5 BRIGHT LIGHT("UNMNRALL") A_CustomRailgun(random(10,20), -32, "black", "red", RGF_NOPIERCING, 1, 2, "CompanionBulletPuff", 0, 0, 1000, random(8,16), 8.0, 2.0, "FatRail", 0);
      UNMB CD 5 A_FaceTarget;
      UNMB E 0 Bright A_StartSound("UNMNRAIL");
      UNMB E 5 BRIGHT LIGHT("UNMNRALR") A_CustomRailgun(random(10,20), 32, "black", "red", RGF_NOPIERCING, 1, 2, "CompanionBulletPuff", 0, 0, 1000, random(8,16), 8.0, 2.0, "FatRail", 0);
      UNMB E 5 BRIGHT LIGHT("UNMNRALL") A_CustomRailgun(random(10,20), -32, "red", "black", RGF_NOPIERCING, 1, 2, "CompanionBulletPuff", 0, 0, 1000, random(8,16), 8.0, 2.0, "FatRail", 0);
      UNMB CD 5 A_FaceTarget;
      Goto See;
    Pain:
      UNMB F 4;
      UNMB F 4 A_Pain;
      Goto See;
    Death:
      TNT1 A 0
      {
        //A_ChangeFlag(FLOATBOB, 0);
        bFloatBob = false;
      }
      UNMB G 7 A_SetFloorClip;
      UNMB H 6 A_Scream;
      UNMB I 6 A_NoBlocking;
      UNMB JKLM 5 A_UnsetSolid;
      UNMB N -1 A_BossDeath;
      Stop;
    Raise:
      UNMB N 6 A_UnSetFloorClip;
      UNMB MLKJIHG 6 A_SetSolid;
      TNT1 A 0
      {
        bFloatBob = true;
      }
      Goto See;
	}
}

Class FatRail : Blood
{
  Default
  {
    +NOGRAVITY
    Mass 0;
    RenderStyle "Translucent";
    Alpha 0.6;
  }
}

Class RedPuff : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Radius 0;
    Height 1;
    +NOGRAVITY
    +NOBLOCKMAP
    +FORCEXYBILLBOARD
    +DontHarmSpecies
    -HarmFriends
    -DoHarmSpecies
    RENDERSTYLE "ADD";
    ALPHA 0.85;
  }
  States
	{
    Spawn:
      TNT1 A 3 Bright;
      RPUF ABCDE 3 Bright;
      Stop;
	}
}

Class FatMace : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Radius 8;
    Height 8;
    Speed 30;
    Damage 4;
    RENDERSTYLE "Normal";
    PROJECTILE;
    +THRUGHOST
    +FORCEXYBILLBOARD
    Seesound "UNMBMACI";
    DeathSound "UNMBMACX";
  }
  States
  {
    Spawn:
      RIP1 ABC 3 Bright A_SpawnItemEx("RedPuff",0,0,0,0,0,0,0,8);
      loop;
    Death:
      RIP1 D 0 Bright A_SetTranslucent (0.75,1);
      RIP1 D 1 Bright A_Explode(64,64, XF_EXPLICITDAMAGETYPE, false, 0, 0, 10, "CompanionBulletPuff", "Companion");
      RIP1 D 0 Bright A_SpawnItemEx("FatMace2",0,0,0,40,0,0,0,0,0);
      RIP1 D 0 Bright A_SpawnItemEx("FatMace2",0,0,0,40,0,0,60,0,0);
      RIP1 D 0 Bright A_SpawnItemEx("FatMace2",0,0,0,40,0,0,120,0,0);
      RIP1 D 0 Bright A_SpawnItemEx("FatMace2",0,0,0,40,0,0,180,0,0);
      RIP1 D 0 Bright A_SpawnItemEx("FatMace2",0,0,0,40,0,0,240,0,0);
      RIP1 D 0 Bright A_SpawnItemEx("FatMace2",0,0,0,40,0,0,300,0,0);
      RIP1 DEFGH 3 Bright;
      Stop;
   }
}

Class FatMace2 : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Radius 5;
    Height 5;
    Damage 1;
    PROJECTILE;
    RENDERSTYLE "Normal";
    +RIPPER
    +THRUGHOST
    +FORCEXYBILLBOARD
    DeathSound "UNMBFIR2";
  }
  States
	{
    Spawn:
      RIP2 A 3 Bright;
      RIP2 B 3 Bright;
      loop;
    Death:
      RIP2 C 0 Bright A_SetTranslucent (0.67,1);
      RIP2 CDEFG 3 Bright;
      Stop;
	}
}

Class HFSpot : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Radius 4;
    Height 4;
    ReactionTime 35;
    RENDERSTYLE "ADD";
    Alpha 0.80;
    Scale 2.0;
    +NOGRAVITY
    +SPAWNCEILING
    +CEILINGHUGGER
    +NOTELEPORT
    +MISSILE
    +DROPOFF
    +NOBLOCKMAP
  }
  States
	{
    Spawn:
      MPOR A 0 Bright;
      MPOR A 0 Bright ThrustThingZ(0,1024,0,0);
      MPOR A 2 Bright ThrustThing(random(0,255),1,0,0);
      MPOR A 0 Bright A_SpawnProjectile("FMaceBall",0,0,random(0,359),6,random(-90,-45));
      MPOR A 0 Bright A_SpawnProjectile("FMaceBall",0,0,random(0,359),6,random(-90,-45));
      MPOR B 2 Bright A_CountDown;
      loop;
    Death:
      MPOR A 0 Bright A_SpawnProjectile("FMaceBall",0,0,random(0,359),6,random(-90,-45));
      MPOR A 0 Bright A_SpawnProjectile("FMaceBall",0,0,random(0,359),6,random(-90,-45));
      MPOR ABABAB 2 Bright A_FadeOut(0.10);
      Stop;
	}
}

Class FMaceBall : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Radius 8;
    Height 4;
    Speed 12;
    Damage 4;
    RENDERSTYLE "Normal";
    PROJECTILE;
    +THRUGHOST
    +FORCEXYBILLBOARD
    -NOGRAVITY
    DeathSound "UNMBOOOM";
  }
  States
	{
    Spawn:
      RIP1 ABC 3 Bright A_SpawnItemEx("RedPuff",0,0,0,0,0,0,0,8);
      loop;
    Death:
      MACX A 0 Bright A_SetTranslucent (0.80,1);
      MACX A 3 Bright;
      MACX B 3 Bright A_Explode(32,64, XF_EXPLICITDAMAGETYPE, false, 0, 0, 10, "CompanionBulletPuff", "Companion");
      MACX CDEFGHIJ 3 Bright;
      Stop;
	}
}