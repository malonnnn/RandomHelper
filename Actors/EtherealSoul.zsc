Class EtherealSoul : Actor
{
  Default
  {
    MONSTER;
    +FRIENDLY
    Species "Companion";
    DamageFactor "Companion", 0;
    obituary "%o was raped by a black soul.";
    health 180;
    radius 16;
    height 32;
    mass 400;
    speed 5;
    damage 5;
    painchance 128;
    renderstyle "Add";
    Alpha 0.4;
    attacksound "skull/melee";
    PainSound "nightmare/pain";
    DeathSound "nightmare/death";
    ActiveSound "nightmare/active";
    +FLOAT
    +NOGRAVITY
    +NOICEDEATH
    +MISSILEMORE
    +DONTFALL
    +STEALTH
    +NOBLOOD
  }

  states
  {
    Spawn:
      ETHS ABCD 10 bright A_Look();
      loop;
    See:
      ETHS AABBCCDD 3 bright A_Chase();
      loop;
    Missile:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      ETHS C 0 bright A_Jump(128, "Missile2");
      ETHS C 6 bright A_FaceTarget();
      ETHS EFEF 4 bright A_SkullAttack();
      goto See;
    Missile2:
      ETHS P 3 bright A_FaceTarget();
      ETHS Q 4 bright A_FaceTarget();
      ETHS R 5 bright A_SpawnProjectile("ShadowBall", 0, 0, 0);
      ETHS E 4 bright;
      goto See;
    Pain:
      ETHS G 3 bright;
      ETHS G 3 bright A_Pain();
      goto See;
    Death:
      ETHS F 3 bright;
      ETHS H 3 bright A_ScreamAndUnblock();
      ETHS IJKLMNO 3 bright;
      stop;
  }

  override bool Slam(Actor victim)
  {
    bSkullFly = false;
    vel = (0, 0, 0);
    if (health > 0)
    {
      if (!bDormant)
      {
        int dam = GetMissileDamage (7, 1);
        int newdam = victim.DamageMobj (self, self, dam, "Companion");
        victim.TraceBleed (newdam > 0 ? newdam : dam, self);
        if (health > 0)
        {
          if (SeeState != NULL && !bRetargetAfterSlam){
            SetState (SeeState);
          }else{
            SetIdle();
          }
        }
      }
      else
      {
        SetIdle();
        tics = -1;
      }
    }
    return false;
  }
}

Class ShadowBall : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Radius 6;
    Height 8;
    Speed 12;
    Damage 4;
    RenderStyle "ADD";
    Alpha 0.75;
    SeeSound "imp/attack";
    DeathSound "imp/shotx";
    Decal "DoomImpScorch";
    Projectile;
    +RANDOMIZE
  }

  States
  {
  Spawn:
    SBAL ABC 4 BRIGHT A_SpawnItemEx("ShadowTrail", 0,0,0, 0,0,0, 0, SXF_CLIENTSIDE);
    Loop;
  Death:
    SBAL C 5 Bright;
    SBAL DEFGH 4 BRIGHT;
    Stop;
  }
}

Class ShadowTrail : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Radius 1;
    Height 1;
    Speed 0;
    RENDERSTYLE "ADD";
    ALPHA 0.6;
    PROJECTILE;
    +NOCLIP
  }

  States
  {
  Spawn:
    SHTR ABCDEF 4 Bright;
    Stop;
  }
}
