//Hades Elemental
Class HadesElemental : Actor
{
  Default
  {
    Monster;
    +FRIENDLY
    Species "Companion";
    DamageFactor "Companion", 0;
    Obituary "%o was electrocuted by a Hades-Elemental.";
    HitObituary "%o got chomped by a Hades-Elemental.";
    Health 1600;
    Radius 34;
    Height 64;
    Mass 800;
    Speed 10;
    PainChance 32;
    SeeSound "monster/helsit";
    PainSound "monster/helpai";
    DeathSound "monster/heldth";
    ActiveSound "monster/helact";
    +Float
    +NoGravity
    +Boss
    +MissileMore
    +Notarget
    +DontHarmClass
    +NoRadiusDmg
  }

  States
  {
    Spawn:
      HELE A 10 A_Look();
      Loop;
    See:
      HELE A 0 A_SetShootable();
      HELE AAAAAAAAAAAA 3 A_Chase();
      HELE A 0 A_Jump(72, "Teleport");
      Loop;
    Teleport:
      HELE A 0 A_UnSetshootable();
      HELE A 1 A_StartSound("monster/heltel");
      HELE A 1 A_SetTranslucent(0.90);
      HELE A 1 A_SetTranslucent(0.80);
      HELE A 1 A_SetTranslucent(0.70);
      HELE A 1 A_SetTranslucent(0.60);
      HELE A 1 A_SetTranslucent(0.50);
      HELE A 1 A_SetTranslucent(0.40);
      HELE A 1 A_SetTranslucent(0.30);
      HELE A 1 A_SetTranslucent(0.20);
      HELE A 1 A_SetTranslucent(0.10);
      TNT1 A 0 A_Jump(128,25);
      TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_Wander();
      TNT1 A 0 A_Jump(128,25);
      TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_Chase(null, null, CHF_NIGHTMAREFAST);
      HELE A 1 A_StartSound("monster/heltel");
      HELE A 1 A_SetTranslucent(0.10);
      HELE A 1 A_SetTranslucent(0.20);
      HELE A 1 A_SetTranslucent(0.30);
      HELE A 1 A_SetTranslucent(0.40);
      HELE A 1 A_SetTranslucent(0.50);
      HELE A 1 A_SetTranslucent(0.60);
      HELE A 1 A_SetTranslucent(0.70);
      HELE A 1 A_SetTranslucent(0.80);
      HELE A 1 A_SetTranslucent(0.90);
      HELE A 1 A_SetTranslucent(1.0);
      HELE A 0 A_SetShootable();
      Goto See;
    Missile:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      HELE E 0 A_Jump(80, "Missile2");
      HELE EF 5 Bright A_Facetarget();
      HELE G 5 Bright
      {
        A_SpawnProjectile("HadesBall",24,0,-10,0,0);
        A_SpawnProjectile("HadesBall",24,0, -5,0,0);
        A_SpawnProjectile("HadesBall",24,0,  0,0,0);
        A_SpawnProjectile("HadesBall",24,0,  5,0,0);
        A_SpawnProjectile("HadesBall",24,0, 10,0,0);
      }
      Goto See+1;
    Missile2:
      HELE E 0 A_Jump(128, "Missile3");
      HELE EF 5 Bright A_Facetarget();
      HELE G 5 Bright
      {
        A_SpawnProjectile("HadesBolt",32,0,-16,0,0);
        A_SpawnProjectile("HadesBolt",32,0,  0,0,0);
        A_SpawnProjectile("HadesBolt",32,0, 16,0,0);
      }
      Goto See+1;
    Missile3:
      HELE BC 5 Bright A_Facetarget();
      HELE D 5 Bright A_CompanionDualPainAttack("HS");
      Goto See+1;
    Melee:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      HELE ABD 5 Bright A_Facetarget();
      HELE C 5 Bright A_CustomMeleeAttack(10*random(1,8), "caco/melee", "Companion");
      Goto See+1;
    Pain:
      HELE H 3;
      HELE H 9 A_Pain();
      Goto See;
    Death:
      HELE I 5 Bright A_Scream();
      HELE JKL 5 Bright;
      HELE M 5 Bright A_NoBlocking();
      HELE N 5 Bright
      {
        A_SpawnItemEx("HS", 36, 36);
        A_SpawnItemEx("HS",-36, 36);
        A_SpawnItemEx("HS",-36,-36);
        A_SpawnItemEx("HS", 36,-36);
      }
      HELE OP 5 Bright;
      HELE QRSTU 5;
      HELE V -1 A_SetFloorClip();
      Stop;
  }

  void A_CompanionDualPainAttack(class<Actor> spawntype = "HS")
	{
		if (target)
		{
			A_FaceTarget();
			A_CompanionPainShootSkull(spawntype, angle + 45);
			A_CompanionPainShootSkull(spawntype, angle - 45);
		}
	}

  void A_CompanionPainShootSkull(Class<Actor> spawntype, double angle, int flags = 0, int limit = -1)
	{
		if (DamageType == "Massacre") return;

		if (spawntype == null) spawntype = "HS";

		if (pos.z + height + 8 > ceilingz)
		{
			if (bFloat)
			{
				Vel.Z -= 2;
				bInFloat = true;
				bVFriction = true;
			}
			return;
		}

		if (limit < 0 && (Level.compatflags & COMPATF_LIMITPAIN))
			limit = 21;

		if (limit > 0)
		{
			int count = limit;
			ThinkerIterator it = ThinkerIterator.Create(spawntype);
			Thinker othink;

			while ( (othink = it.Next ()) )
			{
				if (--count == 0)
					return;
			}
		}

		double otherradius = GetDefaultByType(spawntype).radius;
		double prestep = 4 + (radius + otherradius) * 1.5;

		Vector2 move = AngleToVector(angle, prestep);
		Vector3 spawnpos = pos + (0,0,8);
		Vector3 destpos = spawnpos + move;

		//Actor other = Spawn(spawntype, spawnpos, ALLOW_REPLACE);
    let g = Globals(EventHandler.Find("Globals"));
    Actor other = g.createCompanion("HS", pos, null, "", true);

		if (other != null)
		{
			double maxmove = other.radius - 1;

			if (maxmove <= 0) maxmove = 16;

			double xspeed = abs(move.X);
			double yspeed = abs(move.Y);

			int steps = 1;

			if (xspeed > yspeed)
			{
				if (xspeed > maxmove)
				{
					steps = int(1 + xspeed / maxmove);
				}
			}
			else
			{
				if (yspeed > maxmove)
				{
					steps = int(1 + yspeed / maxmove);
				}
			}

			Vector2 stepmove = move / steps;
			bool savedsolid = bSolid;
			bool savednoteleport = other.bNoTeleport;

			bSolid = false;
			other.bNoTeleport = true;
			for (int i = 0; i < steps; i++)
			{
				Vector2 ptry = other.pos.xy + stepmove;
				double oldangle = other.angle;
				if (!other.TryMove(ptry, 0))
				{
					other.ClearCounters();
					other.DamageMobj(self, self, TELEFRAG_DAMAGE, "Companion");
					bSolid = savedsolid;
					other.bNoTeleport = savednoteleport;
					return;
				}

				if (other.pos.xy != ptry)
				{
					double anglediff = deltaangle(oldangle, other.angle);

					if (anglediff != 0)
					{
						stepmove = RotateVector(stepmove, anglediff);
					}
				}

			}
			bSolid = savedsolid;
			other.bNoTeleport = savednoteleport;

			other.CopyFriendliness (self, !(flags & PAF_NOTARGET));

			if (!(flags & PAF_NOSKULLATTACK))
			{
				other.A_SkullAttack();
			}
		}
	}

  override bool Slam(Actor victim)
  {
    bSkullFly = false;
    vel = (0, 0, 0);
    if (health > 0)
    {
      if (!bDormant)
      {
        int dam = GetMissileDamage (7, 1);
        int newdam = victim.DamageMobj (self, self, dam, "Companion");
        victim.TraceBleed (newdam > 0 ? newdam : dam, self);
        if (health > 0)
        {
          if (SeeState != NULL && !bRetargetAfterSlam){
            SetState (SeeState);
          }else{
            SetIdle();
          }
        }
      }
      else
      {
        SetIdle();
        tics = -1;
      }
    }
    return false;
  }
}

Class HadesBall : CompanionCacodemonBall
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Damage 8;
    Speed 15;
    Alpha 0.80;
    SeeSound "Monster/hadtel";
    DeathSound "Monster/hadsit";
    Decal "CacoScorch";
    +ThruGhost
    +ForceXYBillboard
  }

  States
  {
    Spawn:
      HEFX AB 4 Bright;
      Loop;
    Death:
      HEFX CDEEFGH 3 bright;
      Stop;
  }
}

Class HadesBolt : CompanionCacodemonBall
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Damage 5;
    Speed 5;
    Radius 8;
    Height 8;
    SeeSound "weapons/none";
    DeathSound "weapons/gntidl";
    YScale 4.0;
    XScale 2.0;
    ReactionTime 35;
    +FloorHugger
    +HexenBounce
    +Ripper
    -NoGravity
    -StrifeDamage
  }

  States
  {
    Spawn:
      LFX2 F 1 Bright A_Explode(16,64, XF_EXPLICITDAMAGETYPE, false, 0, 0, 10, "CompanionBulletPuff", "Companion");
      LFX2 F 0 Bright A_SpawnProjectile("HadesBolt2",0,0,0, CMF_AIMDIRECTION | CMF_TRACKOWNER,-90);
      LFX2 F 0 ThrustThing(random(0,255),1,0,0);
      LFX2 G 1 Bright A_Explode(16,64, XF_EXPLICITDAMAGETYPE, false, 0, 0, 10, "CompanionBulletPuff", "Companion");
      LFX2 G 0 Bright A_SpawnProjectile("HadesBolt2",0,0,0, CMF_AIMDIRECTION | CMF_TRACKOWNER,-90);
      LFX2 H 1 Bright A_Explode(16,64, XF_EXPLICITDAMAGETYPE, false, 0, 0, 10, "CompanionBulletPuff", "Companion");
      LFX2 H 0 Bright A_SpawnProjectile("HadesBolt2",0,0,0, CMF_AIMDIRECTION | CMF_TRACKOWNER,-90);
      LFX2 I 1 Bright A_Explode(16,64, XF_EXPLICITDAMAGETYPE, false, 0, 0, 10, "CompanionBulletPuff", "Companion");
      LFX2 I 0 Bright A_SpawnProjectile("HadesBolt2",0,0,0, CMF_AIMDIRECTION | CMF_TRACKOWNER,-90);
      LFX2 J 1 Bright A_Explode(16,64, XF_EXPLICITDAMAGETYPE, false, 0, 0, 10, "CompanionBulletPuff", "Companion");
      LFX2 J 0 Bright A_SpawnProjectile("HadesBolt2",0,0,0, CMF_AIMDIRECTION | CMF_TRACKOWNER,-90);
      LFX2 J 0 Bright A_CountDown();
      Loop;
    Death:
      LFX2 FGHIJ 2 Bright A_Explode(32,64, XF_EXPLICITDAMAGETYPE, false, 0, 0, 10, "CompanionBulletPuff", "Companion");
      Stop;
  }
}

Class HadesBolt2 : CompanionCacodemonBall
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Damage 0;
    Speed 184;
    RenderStyle "None";
    SeeSound "Monster/hadtel";
    DeathSound "Monster/hadsit";
    +ThruGhost
    +Ripper
  }

  States
  {
  Spawn:
    TNT1 A 1 Bright A_Explode(16,64, XF_EXPLICITDAMAGETYPE, false, 0, 0, 10, "CompanionBulletPuff", "Companion");
    Loop;
  Death:
    TNT1 A 1 Bright;
    Stop;
  }
}
//End of Hades Elemental

