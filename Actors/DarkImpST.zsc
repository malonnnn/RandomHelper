Class DarkImpST : Actor
{
  Default
  {
    MONSTER;
    +FRIENDLY
    Species "Companion";
    DamageFactor "Companion", 0;
    Health 110;
    Radius 20;
    Height 56;
    Speed 9;
    PainChance 200;
    SeeSound "imp/sight";
    PainSound "imp/pain";
    DeathSound "imp/death";
    ActiveSound "imp/active";
    Obituary "%o was cursed by a dark imp.";
    HitObituary "%o was touched by a dark imp.";
    +FLOORCLIP
  }

  States
  {
    Spawn:
      DRKS AB 10 A_Look();
      Loop;
    See:
      DRKS AABBCCDD 3 A_Chase();
      Loop;
    Melee:
    Missile:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);  
      DRKS EF 8 A_FaceTarget();
      DRKS G 6 A_CustomComboAttack("DarkFast", 32, 3*random(1,8), "imp/melee", "Companion");
      Goto See;
    Pain:
      DRKS H 2;
      DRKS H 2 A_Pain();
      Goto See;
    Death:
      DRKS I 8;
      DRKS J 8 A_Scream();
      DRKS K 6;
      DRKS L 6 A_Fall();
      DRKS M -1;
      Stop;
    XDeath:
      DRKI N 5;
      DRKI O 5 A_XScream();
      DRKI P 5;
      DRKI Q 5 A_Fall();
      DRKI RST 5;
      DRKI U -1;
      Stop;
    Raise:
      DRKS ML 8;
      DRKS KJI 6;
      Goto See;
  }
}

Class DarkFast : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    PROJECTILE;
    Radius 8;
    Height 6;
    Speed 18;
    Damage 3;
    RENDERSTYLE "ADD";
    ALPHA 0.67;
    SeeSound "imp/attack";
    DeathSound "imp/shotx";
    Decal "DoomImpScorch";
  }

  States
  {
    Spawn:
      DKB1 AB 4 Bright;
      Loop;
    Death:
      DKB1 CDE 4 Bright;
      Stop;
  }
}

