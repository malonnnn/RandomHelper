Class Inferno : Actor
{
  //Pointers to the projectiles we throw in death state, so obituaries for those work after it dies.
  Actor projA;
  Actor projB;
  Actor projC;
  Actor projD;

  Default
  {
    +FRIENDLY
    Species "Companion";
    DamageFactor "Companion", 0;
    Health 400;
    Radius 31;
    Height 56;
    Mass 400;
    Speed 10;
    PainChance 128;
    DamageFactor "Fire", 0.5;
    HITOBITUARY "%o got eaten by an inferno.";
    OBITUARY "%o got incinerated by an inferno.";
    SEESOUND "monster/infsit";
    PAINSOUND "monster/infpai";
    DEATHSOUND "monster/infdth";
    ACTIVESOUND "caco/active";
    MONSTER;
    +NoGravity
    +Float
    +DontFall
    +DontHarmClass
  }

  states
  {
    Spawn:
      INFR A 10 A_Look();
      Loop;
    See:
      INFR A 3 A_Chase();
      Loop;
    Missile:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      INFR BC 5 A_FaceTarget();
      INFR D 5 Bright A_SpawnProjectile("Lavaball",24,0,0,0);
      Goto See;
    Melee:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      INFR ADC 5 A_FaceTarget();
      INFR B 5 A_CustomMeleeAttack(8*Random(1, 8), "Caco/Melee", "", "Companion");
      Goto See;
    Pain:
      INFR E 3;
      INFR E 3 A_Pain();
      INFR E 6;
      Goto See;
    Death:
      INFR G 8;
      INFR H 8 Bright A_Scream();
      INFR I 8 Bright;
      INFR J 8 Bright
      {
        projA = A_SpawnProjectile("Lavaball",32,0, 45, CMF_AIMDIRECTION);
        projB = A_SpawnProjectile("Lavaball",32,0,135, CMF_AIMDIRECTION);
        projC = A_SpawnProjectile("Lavaball",32,0,225, CMF_AIMDIRECTION);
        projD = A_SpawnProjectile("Lavaball",32,0,315, CMF_AIMDIRECTION);
      }
      INFR K 8 Bright A_NoBlocking();
      INFR K 0 A_SetFloorClip();
      INFR K 0 { bDontGib = true; } //Let's make sure it doesn't leave a bloodstain when it's supposed to be basically invisible
      //Stop; //This is the old end to the Death state.
    Disappear:
      TNT1 A 10; //Wait until the projectiles we threw are gone, then disappear
      TNT1 A 0
      {
        if( projA || projB || projC || projD ) //If any of our projectiles are still around,
          return ResolveState("Disappear");
        return ResolveState(null);
      }
      Stop;
  }
}

Class Lavaball : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Radius 8;
    Height 8;
    Speed 15;
    Damage 6;
    RenderStyle "Add";
    Alpha 0.95;
    SeeSound "weapons/firmfi";
    DeathSound "weapons/firex3";
    PROJECTILE;
    +THRUGHOST
  }

  States
  {
    Spawn:
      BAL3 AB 4 Bright A_SpawnItemEX("RedPuff", 0,0,0, 0,0,0, 0, SXF_CLIENTSIDE);
      loop;
    Death:
      BAL3 C 5 Bright A_Explode(32, 96, XF_EXPLICITDAMAGETYPE, false, 0, 0, 10, "CompanionBulletPuff", "Companion");
      BAL3 DE 5 Bright;
      stop;
  }
}

