Class FemaleZombie : Actor
{
  Default
  {
    MONSTER;
    +FRIENDLY
    Species "Companion";
    DamageFactor "Companion", 0;
    obituary "%o was killed by a female zombie.";
    health 15;
    mass 90;
    speed 10;
    Radius 19;
    Height 52;
    painchance 200;
    seesound "fem/sight";
    attacksound "grunt/attack";
    painsound "fem/pain";
    deathsound "fem/death";
    activesound "fem/active";
    dropitem "Clip", 256;
    +FLOORCLIP
  }
  States
  {
    Spawn:
      ZOMW AB 10 A_Look;
      loop;
    See:
      ZOMW AABBCCDD 4 A_Chase;
      loop;
    Missile:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      ZOMW E 10 A_FaceTarget;
      ZOMW F 8 A_CompanionPosAttack;
      ZOMW E 8;
      goto See;
    Pain:
      ZOMW G 3;
      ZOMW G 3 A_Pain;
      goto See;
    Death:
      ZOMW H 5;
      ZOMW I 5 A_Scream;
      ZOMW J 5 A_NoBlocking;
      ZOMW K 5;
      ZOMW L 5;
      ZOMW M 5;
      ZOMW N -1;
      stop;
    XDeath:
      ZOMW O 5;
      ZOMW P 5 A_XScream;
      ZOMW Q 5 A_NoBlocking;
      ZOMW RSTUV 5;
      ZOMW W -1;
      stop;
    Raise:
      ZOMW MLKJIH 5;
      goto See;
  }

  void A_CompanionPosAttack()
	{
		if (target)
		{
			A_FaceTarget();
			double ang = angle;
			double slope = AimLineAttack(ang, MISSILERANGE);
			A_StartSound("grunt/attack", CHAN_WEAPON);
			ang  += Random2[PosAttack]() * (22.5/256);
			int damage = Random[PosAttack](1, 5) * 3;
			LineAttack(ang, MISSILERANGE, slope, damage, "Companion", "CompanionBulletPuff");
		}
	}
}
