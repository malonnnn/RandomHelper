Class HellWarrior : Actor
{
  Default
  {
    Monster;
    +FRIENDLY
    Species "Companion";
    DamageFactor "Companion", 0;
    Health 400;
    Speed 8;
    Radius 24;
    Height 64;
    PainChance 50;
    Mass 1000;
    SeeSound "knight/sight";
    PainSound "knight/pain";
    DeathSound "knight/death";
    ActiveSound "knight/active";
    ReactionTime 8;
    Obituary "%o was tagged by the hell warrior's fireball.";
    HitObituary "%o was torn apart by a hell warrior.";
    +FloorClip
  }

  States
  {
    Spawn:
      HWAR AB 10 A_Look();
      Loop;
    See:
      HWAR A 0 A_UnSetInvulnerable();
      HWAR AABBCCDD 3 A_Chase();
      goto See+1;
    Missile:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      HWAR E 0 A_Jump(128, "Missile2");
      HWAR EF 8 A_FaceTarget();
      HWAR G 8 A_CustomComboAttack("HellWarriorBall", 32, 8, "Baron/Melee");
      Goto See;
    Missile2:
      HWAR H 0 A_Jump(128, "Missile3");
      HWAR H 0 A_SetInvulnerable();
      HWAR H 6 A_FaceTarget();
      HWAR I 4 Bright A_SpawnProjectile("HellWarriorBall2", 25, 0, 0);
      HWAR H 6 A_FaceTarget();
      HWAR H 0 A_UnSetInvulnerable();
      Goto See;
    Missile3:
      HWAR H 0 A_SetInvulnerable();
      HWAR H 6 A_FaceTarget();
      HWAR I 4 Bright A_SpawnProjectile("HellWarriorBall2", 25, 0, 0);
      HWAR H 16 A_SpidRefire();
      HWAR I 4 Bright A_SpawnProjectile("HellWarriorBall2", 25, 0, 0);
      HWAR H 6 A_FaceTarget();
      HWAR H 0 A_UnSetInvulnerable();
      Goto See;
    Melee:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      HWAR EF 8 A_FaceTarget();
      //HWAR G 8 A_BasicAttack(8, "Baron/Melee", "HellWarriorBall", 32);
      HWAR G 8 A_CustomComboAttack("HellWarriorBall", 32, 8, "Baron/Melee");
      Goto See;
    Pain:
      HWAR J 6 A_Pain();
      HWAR H 0 A_SetInvulnerable();
      HWAR HHHHHHH 6 A_FaceTarget();
      HWAR H 0 A_UnSetInvulnerable();
      Goto See;
    Death:
      HWAR K 0 A_FaceTarget();
      HWAR K 5 A_SpawnItemEx("HellWarriorShield", 0, 0, 25, 6, 0, 0, 60, 128);
      HWAR L 5 A_Scream();
      HWAR M 5;
      HWAR N 5 A_NoBlocking();
      HWAR OPQRS 5;
      HWAR T -1;
      Stop;
    Raise:
      HWAR QPONMLK 5;
      Goto See;
  }
}

Class HellWarriorBall : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Radius 6;
    Height 8;
    Speed 14;
    Damage 8;
    RenderStyle "Add";
    Alpha 0.7;
    SeeSound "imp/attack";
    DeathSound "imp/shotx";
    Decal "BaronScorch";
    Projectile;
  }

  States
  {
    Spawn:
      HWFB AB 5 Bright;
      Loop;
    Death:
      HWFB CDE 5 Bright;
      Stop;
  }
}

Class HellWarriorBall2 : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Radius 6;
    Height 8;
    Speed 8;
    Damage 2;
    RenderStyle "Add";
    Alpha 0.8;
    Scale 0.4;
    SeeSound "imp/attack";
    DeathSound "imp/shotx";
    Projectile;
    +Randomize
    +Ripper
  }

  States
  {
    Spawn:
      GRBA ABCDEFGH 2 Bright;
      Loop;
    Death:
      GRBA AIJKLMN 3 Bright;
      Stop;
  }
}

Class HellWarriorShield : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Radius 8;
    Height 8;
    Speed 6;
    +DoomBounce;
    +DropOff;
    +Missile;
  }

  States
  {
    Spawn:
      HWSH ABCDEFGH 3;
      Loop;
    Death:
      HWSH I -1;
      Loop;
  }
}

