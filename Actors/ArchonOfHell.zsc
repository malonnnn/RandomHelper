Class ArchonOfHell : BaronOfHell
{
  Default
  {
    MONSTER;
    +Friendly;
    DamageFactor "Companion", 0;
    Health 2000;
    Radius 24;
    Height 64;
    Mass 1000;
    Speed 8;
    PainChance 20;
    Obituary "%o was no match for an archon of hell.";
    HitObituary "%o was ripped apart by an archon of hell.";
    BloodColor "20 60 20";
    SeeSound "monster/ar2sit";
    PainSound "baron/pain";
    DeathSound "monster/ar2dth";
    +NoTarget
    +FloorClip
    +MissileMore
    +NoRadiusDmg
    +Boss
    +DontHarmClass
  }

  States
  {
  Spawn:
    BOS4 AB 10 A_Look();
    Loop;
  See:
    BOS4 AABBCCDD 3 A_Chase();
    Loop;
  Missile:
    TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
    BOS4 B 0 A_Jump(128, "Comet");
    BOS4 EF 8 Bright A_FaceTarget();
    BOS4 G 3 Bright
    {
      A_SpawnProjectile("CompanionBaronBall",28,0,0,0);
      A_SpawnProjectile("CompanionBaronBall",28,0,-4,0);
      A_SpawnProjectile("CompanionBaronBall",28,0,4,0);
    }
    BOS4 H 3 Bright;
    BOS4 H 0 A_Jump(64, "Comet");
    BOS4 IJ 8 Bright A_FaceTarget;
    BOS4 K 3 Bright
    {
      A_SpawnProjectile("CompanionBaronBall",28,0,0,0);
      A_SpawnProjectile("CompanionBaronBall",28,0,-4,0);
      A_SpawnProjectile("CompanionBaronBall",28,0,4,0);
    }
    BOS4 L 3 Bright;
    BOS4 L 0 A_Jump(64, "Comet");
    Goto See;
  Comet:
    BOS4 MN 8 Bright A_FaceTarget;
    BOS4 O 0 Bright A_Jump(128, "Wave");
    BOS4 O 4 Bright A_SpawnProjectile("ArchonComet",28,0,0,0);
    BOS4 P 4 Bright;
    Goto See;
  Wave:
    BOS4 O 3 Bright
    {
      A_SpawnProjectile("CompanionBaronBall",28,0,-6,0);
      A_SpawnProjectile("CompanionBaronBall",28,0,-4,0);
      A_SpawnProjectile("CompanionBaronBall",28,0,-2,0);
      A_SpawnProjectile("CompanionBaronBall",28,0,2,0);
      A_SpawnProjectile("CompanionBaronBall",28,0,4,0);
      A_SpawnProjectile("CompanionBaronBall",28,0,6,0);
    }
    BOS4 P 3 Bright;
    Goto See;
  Melee:
    TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
    BOS4 EF 8 Bright A_FaceTarget();
    BOS4 G 3 Bright A_CustomMeleeAttack(10*Random(1,8), "baron/melee", "", "Companion");
    BOS4 H 3 Bright;
    BOS4 H 0 A_Jump(128, "Melee2");
    Goto See;
  Melee2:
    BOS4 IJ 8 Bright A_FaceTarget();
    BOS4 K 3 Bright A_CustomMeleeAttack(10*Random(1,8), "baron/melee", "", "Companion");
    BOS4 L 3 Bright;
    BOS4 L 0 A_Jump(64, "Melee3");
    Goto See;
  Melee3:
    BOS4 MN 8 Bright A_FaceTarget();
    BOS4 O 3 Bright
    {
      A_CustomMeleeAttack(10*Random(1,8), "baron/melee", "", "Companion");
      A_CustomMeleeAttack(10*Random(1,8), "baron/melee", "", "Companion");
    }
    BOS4 P 3 Bright;
    Goto See;
  Pain:
    BOS4 Q 2;
    BOS4 Q 2 A_Pain();
    Goto See;
  Death:
    BOS4 R 6 Bright;
    BOS4 S 6 Bright A_Scream();
    BOS4 T 6 Bright;
    BOS4 U 6 Bright A_NoBlocking();
    BOS4 VW 6 Bright;
    BOS4 X 6 Bright A_SpawnProjectile("ArchonSoul",86,0,0,2,-90);
    BOS4 YZ 6 Bright;
    BOS4 [ 6 Bright;
    Stop;
  }
}

Class ArchonComet : Actor
{
  Default
  {
    Radius 8;
    Height 12;
    Speed 25;
    Damage 20;
    Scale 1.0;
    SeeSound "weapons/firbfi";
    DeathSound "weapons/hellex";
    PROJECTILE;
    Species "Companion";
    DamageType "Companion";
    +ThruGhost
  }

  States
  {
  Spawn:
    ARCB AAAABBBBCCCC 1 Bright A_SpawnItemEx("ArchonCometTrail");
    Loop;
  Death:
    ARCB J 0 Bright A_SetTranslucent(0.67,1);
    ARCB J 3 Bright;
    ARCB K 3 Bright A_Explode(128,128,XF_EXPLICITDAMAGETYPE, false, 0, 0, 10, "CompanionBulletPuff", "Companion");
    ARCB LMN 3 Bright;
    Stop;
  }
}

Class ArchonSoul : Actor
{
  Default
  {
    Radius 1;
    Height 1;
    Speed 3;
    RenderStyle "Add";
    Alpha 0.80;
    PROJECTILE;
    Species "Companion";
    DamageType "Companion";
    +ClientSideOnly
  }

  States
  {
  Spawn:
    BFX1 ABCD 6 Bright;
    Stop;
  }
}

Class ArchonCometTrail : Actor
{
  Default
  {
    Radius 3;
    Height 3;
    Scale 0.75;
    Speed 0;
    RenderStyle "Add";
    Alpha 0.67;
    PROJECTILE;
    Species "Companion";
    DamageType "Companion";
    +ClientSideOnly
  }

  States
  {
  Spawn:
    TNT1 A 3 Bright;
    ARCB DEFGHI 3 Bright;
    Stop;
  }
}

