class Devil : CompanionDoomImp
{
  Default
  {
    MONSTER;
    +FRIENDLY
    Species "Companion";
    DamageFactor "Companion", 0;
    obituary "%o was fried by a Devil.";
    hitobituary "%o was flayed by a Devil.";
    health 120;
    radius 20;
    height 56;
    mass 120;
    speed 10;
    painchance 160;
    seesound "monster/dvlsit";
    painsound "monster/dvlpai";
    deathsound "monster/dvldth";
    activesound "monster/dvlact";
    +FLOORCLIP
  }
  states
  {
    Spawn:
      TRO2 AB 10 A_Look;
      loop;
    See:
      TRO2 AABBCCDD 3 A_Chase;
      loop;
    Melee:
    Missile:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      TRO2 EF 6 A_FaceTarget;
      TRO2 G 4 A_CompanionTroopAttack();
      TRO2 B 2;
      TRO2 VW 6 A_FaceTarget;
      TRO2 X 4 A_CompanionTroopAttack();
      TRO2 E 0 A_Jump(200,9);
      TRO2 D 2;
      TRO2 EF 6 A_FaceTarget;
      TRO2 G 4 A_CompanionTroopAttack();
      TRO2 B 2;
      TRO2 VW 6 A_FaceTarget;
      TRO2 X 4 A_CompanionTroopAttack();
      TRO2 B 0;
      goto See;
    Pain:
      TRO2 H 2;
      TRO2 H 2 A_Pain;
      goto See;
    Death:
      TRO2 I 8;
      TRO2 J 8 A_Scream;
      TRO2 K 6;
      TRO2 L 6 A_NoBlocking;
      TRO2 M -1;
      stop;
    XDeath:
      TRO2 N 5;
      TRO2 O 5 A_XScream;
      TRO2 P 5;
      TRO2 Q 5 A_NoBlocking;
      TRO2 RST 5;
      TRO2 U -1;
      stop;
    Raise:
      TRO2 ML 8;
      TRO2 KJI 6;
      goto See;
  }

  void A_CompanionTroopAttack()
	{
		let targ = target;
		if (targ)
		{
			if (CheckMeleeRange())
			{
				int damage = random[pr_troopattack](1, 8) * 3;
				A_StartSound ("imp/melee", CHAN_WEAPON);
				int newdam = targ.DamageMobj (self, self, damage, "Companion");
				targ.TraceBleed (newdam > 0 ? newdam : damage, self);
			}
			else
			{
        A_SpawnProjectile("CompanionDoomImpBall");
			}
		}
	}
}

