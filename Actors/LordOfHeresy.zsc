Class LordOfHeresy : Actor
{
  Default
  {
    MONSTER;
    +FRIENDLY
    Species "Companion";
    DamageFactor "Companion", 0;
    Health 1250;
    Radius 20;
    Height 56;
    Speed 13;
    PainChance 50;
    BloodColor "7D 9D 59";
    Mass 1000;
    Obituary "%o was annihilated by the heresy lord.";
    SeeSound "LordOfHeresy/sight";
    PainSound "demon/pain";
    DeathSound "LordOfHeresy/death";
    ActiveSound "LordOfHeresy/act";
    +FLOORCLIP
  }

  States
  {
    Spawn:
      LOHS AB 10 A_Look();
      Loop;
    See:
      LOHS AABBCCDD 4 A_Chase();
      Loop;
    Missile:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      LOHS E 0 A_Jump(85, "Missile2");
      LOHS E 0 A_Jump(128, "Missile3");
      LOHS EF 8 A_FaceTarget();
      LOHS G 8 A_CompanionBruisAttack();
      Goto See;
    Missile2:
      LOHS EF 8 A_FaceTarget();
      LOHS G 8
      {
        A_SpawnProjectile("CompanionBaronBall", 32, 0,  8);
        A_SpawnProjectile("CompanionBaronBall", 32, 0,  0);
        A_SpawnProjectile("CompanionBaronBall", 32, 0, -8);
      }
      Goto See;
    Missile3:
      LOHS EF 8 A_FaceTarget();
      LOHS G 8
      {
        A_SpawnProjectile("CompanionBaronBall", 32, 0, -20);
        A_SpawnProjectile("CompanionBaronBall", 32, 0,   0);
      }
      LOHS EF 8 A_FaceTarget();
      LOHS G 8
      {
        A_SpawnProjectile("CompanionBaronBall", 32, 0,  20);
        A_SpawnProjectile("CompanionBaronBall", 32, 0,   0);
      }
      LOHS EF 8 A_FaceTarget();
      LOHS G 8
      {
        A_SpawnProjectile("CompanionBaronBall", 32, 0, -12);
        A_SpawnProjectile("CompanionBaronBall", 32, 0,  12);
      }
      Goto See;
    Pain:
      LOHS H 5;
      LOHS H 5 A_Pain();
      Goto See;
    Death:
      LOHS I 8;
      LOHS J 8 A_Scream();
      LOHS K 8 A_Fall();
      LOHS LMN 8;
      LOHS O -1;
      Stop;
    Raise:
      LOHS ONMLKJI 5;
      Goto See;
  }

  void A_CompanionBruisAttack()
	{
		let targ = target;
		if (targ)
		{
			if (CheckMeleeRange())
			{
				int damage = random[pr_bruisattack](1, 8) * 10;
				A_StartSound ("baron/melee", CHAN_WEAPON);
				int newdam = target.DamageMobj (self, self, damage, "Companion");
				targ.TraceBleed (newdam > 0 ? newdam : damage, self);
			}
			else
			{
        A_SpawnProjectile("CompanionBaronBall");
			}
		}
	}
}
