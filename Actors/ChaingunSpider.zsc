Class ChaingunSpider : Actor
{
  Default
  {
    MONSTER;
    +FRIENDLY
    Species "Companion";
    DamageFactor "Companion", 0;
    obituary "%o was filled full of lead by an Arachnotron.";
    health 450;
    radius 64;
    height 64;
    mass 600;
    speed 12;
    painchance 128;
    seesound "CGunSpider/Sight";
    painsound "baby/pain";
    deathsound "CGunSpider/Death";
    activesound "baby/active";
    attacksound "CGunSpider/Attack";
    dropitem "ClipBox", 64;
    decal "bulletchip";
    +FLOORCLIP
  }
  states
  {
    Spawn:
      CSPI AB 10 A_Look;
      loop;
    See:
      CSPI A 20;
      CSPI A 3 A_BabyMetal;
      CSPI ABBCC 3 A_Chase;
      CSPI D 3 A_BabyMetal;
      CSPI DEEFF 3 A_Chase;
      goto See+1;
    Missile:
      CSPI A 20 bright A_FaceTarget;
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      CSPI G 4 bright A_CompanionCPosAttack;
      CSPI G 0 bright A_CompanionCPosAttack;
      CSPI H 4 bright;
      CSPI H 1 bright A_SpidRefire;
      goto Missile+1;
    Pain:
      CSPI I 3;
      CSPI I 3 A_Pain;
      goto See+1;
    Death:
      CSPI J 20 A_Scream;
      CSPI K 7 A_NoBlocking;
      CSPI LMNO 7;
      CSPI P -1 A_BossDeath;
      stop;
    Raise:
      CSPI PONMLKJ 5;
      goto See+1;
  }

  void A_CompanionCPosAttack()
	{
		if (target)
		{
			if (bStealth) visdir = 1;
			A_StartSound(AttackSound, CHAN_WEAPON);
			A_FaceTarget();
			double slope = AimLineAttack(angle, MISSILERANGE);
			double ang = angle + Random2[CPosAttack]() * (22.5/256);
			int damage = Random[CPosAttack](1, 5) * 3;
			LineAttack(ang, MISSILERANGE, slope, damage, "Companion", "CompanionBulletpuff");
		}
	}
}
