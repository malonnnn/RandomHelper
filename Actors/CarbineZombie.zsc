Class CarbineZombie : Actor
{
  Default
  {
    MONSTER;
    +FRIENDLY
    Species "Companion";
    DamageFactor "Companion", 0;
    Health 80;
    Radius 20;
    Height 56;
    Mass 100;
    Speed 8;
    PainChance 100;
    +MISSILEMORE
    +FLOORCLIP
    SeeSound "MGzombiesee";
    PainSound "MGPPAIN";
    DeathSound "MGzombiedeath";
    ActiveSound "MGPSACT";
    Obituary "%o was shot to pieces by a Heavy Carbine-wielding zombie.";
    Dropitem "DamagedBlueArmor", 40;
  }
  States
  {
    Spawn:
      HCPO AB 10 A_Look;
      Loop;
    See:
      HCPO AABBCCDD 3 A_Chase;
      Loop;
    Missile:
      HCPO E 10 A_FaceTarget;
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      HCPO F 0 A_quake(2,12,0,128,"None");
      HCPO F 0 A_StartSound("HCSHOT",0);
      HCPO F 2 Bright A_SpawnProjectile("CarbineGuyBullet",40,0,random(-4,4),0,random(-4,4));
      HCPO E 2;
      HCPO F 0 A_StartSound("HCSHOT",0);
      HCPO F 2 Bright A_SpawnProjectile("CarbineGuyBullet",40,0,random(-3,3),0,random(-3,3));
      HCPO E 2;
      HCPO F 0 A_StartSound("HCSHOT",0);
      HCPO F 2 Bright A_SpawnProjectile("CarbineGuyBullet",40,0,random(-2,2),0,random(-2,2));
      HCPO E 2;
      HCPO E 1 A_CPosRefire;
      Goto Missile+1;
    Melee:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      HCPO D 8 A_FaceTarget;
      HCPO E 8 A_custommeleeattack(12,"HCSTAB", "", "Companion");
      HCPO D 6;
    Goto See;
    Pain:
      HCPO G 3;
      HCPO G 3 A_Pain;
      Goto See;
    Death:
      HCPO H 5;
      HCPO I 5 A_Scream;
      HCPO J 5 A_NoBlocking;
      HCPO K 5;
      HCPO L -1;
      Stop;
    XDeath:
      HCPO M 5;
      HCPO N 5 A_XScream;
      HCPO O 5 A_NoBlocking;
      HCPO PQR 5;
      HCPO S -1;
      Stop;
    Raise:
      HCPO L 5;
      HCPO KJIH 5;
      Goto See;
  }
}

Class CarbineGuyBullet : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    +DontHarmSpecies
    -HarmFriends
    -DoHarmSpecies
    Radius 2;
    Height 2;
    Speed 50;
    Damage 2;
    Projectile;
    +BLOODSPLATTER
    Decal "bulletchip";
    Obituary "%o was shot to pieces";
  }
  States
  {
    Spawn:
      BLT2 A 1 Bright;
      Loop;
    Death:
      BLT1 A 1 Bright A_spawnitem("CompanionBulletPuff");
      Stop;
    XDeath:
      BLT1 A 0;
      Stop;
  }
}

Class DamagedBlueArmor : BasicArmorPickup
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Radius 20;
    Height 16;
    Inventory.PickupMessage "Salvaged a damaged Mega armour";
    Inventory.Icon "ARM2A0";
    Armor.SavePercent 50;
    Armor.SaveAmount 50;
  }
  States
  {
    Spawn:
      BRAM A 6;
      BRAM B 6 Bright;
      Loop;
  }
}

Class deadcarbineguy1 : Actor
{
  Default
  {
    Height 56;
    Radius 12;
    +SOLID;
  }
  States
  {
    Spawn:
      HCPD A -1;
      Stop;
  }
}

Class deadcarbineguy2 : Actor
{
  Default
  {
    Height 56;
    Radius 12;
  }
  States
  {
    Spawn:
      HCPD B -1;
      Stop;
  }
}
