Class Crash : Actor
{
  Default
  {
    MONSTER;
    +FRIENDLY
    Species "Companion";
    DamageFactor "Companion", 0;
    //translation 2;
    obituary "%o Crashed.";
    health 100;
    radius 20;
    height 56;
    mass 100;
    speed 12;
    painchance 100;
    seesound "crash/sight";
    painsound "crash/pain";
    deathsound "crash/death";
    AttackSound "grunt/attack";
    dropitem "cell", 256;
    +FLOORCLIP
    +NORADIUSDMG
    +DONTMORPH
    +MISSILEMORE
  }
  states
  {
    Spawn:
      CRSH AB 10 A_Look;
      loop;
    See:
      CRSH AABBCCDD 2 A_Chase;
      CRSH A 0 A_jump(80,2);
      loop;
      CRSH AABBCCDDAABBCCDD 2 A_FastChase;
      Goto See;
    Missile:
      CRSH E 10 A_FaceTarget;
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      CRSH FE 4 BRIGHT A_SpawnProjectile("companionplasmaball");
      CRSH F 0 A_CPosRefire;
      Goto Missile+1;
    Pain:
      CRSH G 3;
      CRSH G 3 A_Pain;
      goto See;
    Death:
      CRSH H 5;
      CRSH I 5 A_Scream;
      CRSH J 5 A_NoBlocking;
      CRSH K 5;
      CRSH LM 5;
      CRSH N -1;
      stop;
    Xdeath:
      CRSH O 5;
      CRSH P 5 A_XScream;
      CRSH Q 5 A_NoBlocking;
      CRSH RSTUV 5;
      CRSH W -1;
      stop;
  }
}
