Class CyberMastermind : Actor
{
  Default
  {
    MONSTER;
    +FRIENDLY
    Species "Companion";
    DamageFactor "Companion", 0;
    Health 5000;
    Radius 80;
    Height 100;
    Speed 12;
    PainChance 30;
    Mass 1000;
    +FLOORCLIP
    +BOSS
    +NORADIUSDMG 
    SeeSound "cyber/sight";
    PainSound "spider/pain";
    DeathSound "cyber/death";
    ActiveSound "spider/active";
    Obituary "%o was destroyed by a cyber mastermind.";
    Decal "BulletChip";
  }
  States
  {
    Spawn:
      CYMM AB 10 A_Look;
      Loop;
    See:
      CYMM AABB 3 A_Chase;
      CYMM B 0 A_StartSound("spider/walk");
      CYMM CCDD 3 A_Chase;
      CYMM D 0 A_StartSound("spider/walk");
      CYMM EEFF 3 A_Chase;
      CYMM F 0 A_StartSound("spider/walk");
      Loop;
    Missile:
      CYMM A 20 A_FaceTarget;
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      CYMM GHGH 4 A_CompanionSPosAttack();
      CYMM H 0 A_FaceTarget;
      CYMM I 0 A_SpawnProjectile("CompanionRocket", 32, 0, 0);
      CYMM IJ 4 A_CompanionSPosAttack();
      CYMM J 1 A_SpidRefire;
      Goto Missile+1;
    Pain:
      CYMM K 4;
      CYMM K 4 A_Pain;
      Goto See;
    Death:
      CYMM L 8;
      CYMM M 8 A_Scream;
      CYMM N 6;
      CYMM O 6 A_Fall;
      CYMM PQRST 6;
      CYMM U -1;
      Stop;
    }

  void A_CompanionSPosAttack()
	{
		if (target)
		{
			A_StartSound("shotguy/attack", CHAN_WEAPON);
			A_CompanionSPosAttackInternal();
		}
	}
  private void A_CompanionSPosAttackInternal()
	{
		if (target)
		{
			A_FaceTarget();
			double bangle = angle;
			double slope = AimLineAttack(bangle, MISSILERANGE);

			for (int i=0 ; i<3 ; i++)
			{
				double ang = bangle + Random2[SPosAttack]() * (22.5/256);
				int damage = Random[SPosAttack](1, 5) * 3;
				LineAttack(ang, MISSILERANGE, slope, damage, "Companion", "CompanionBulletpuff");
			}
		}
  }
}

