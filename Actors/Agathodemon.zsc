//============================================//
//                                            //
//  Agathodemon                               //
//                                            //
//  DECORATE/Recolors By: jimmy91             //
//  Projectile Sprites by: Eriance, Vader     //
//                                            //
//============================================//

Class Agathodemon : Actor
{
  bool stealth;

  Default
  {
    MONSTER;
    +FRIENDLY
    Species "Companion";
    DamageFactor "Companion", 0;
    Radius 31;
    Height 64;
    Health 650;
    Mass 1000;
    Speed 12;
    PainChance 160;
    SeeSound "caco/sight";
    PainSound "caco/pain";
    DeathSound "caco/death";
    ActiveSound "caco/active";
    AttackSound "caco/melee";
    Obituary "%o was blasted by an agathodemon.";
    HitObituary "%o got caught in an agathodemon's jaws.";
    +FLOAT
    +NOGRAVITY
    +MISSILEMORE
  }

  States
  {
    Spawn:
      AGAH A 1 A_Look();
      Loop;
    See:
      AGAH A 3 A_Chase();
      Loop;
    Melee:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      AGAH BC 5 A_FaceTarget();
      AGAH D 6 A_CustomMeleeAttack(12*Random(1,8), "imp/melee", "", "Companion");
      Goto See;
    Missile:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      AGAH D 0 A_Jump(55, "Stealth");
      AGAH D 0 A_Jump(128, "Fireball");
      Goto Tracer;
    Stealth:
      AGAH D 0 A_JumpIf(stealth, "EndStealth");
      AGAH BC 5 Bright A_FaceTarget();
      AGAH D 2 Bright A_StartSound("agatho/stealth");
      AGAH D 0 A_UnSetShootable();
      AGAH DDDDDDDDD 2 Bright A_FadeOut(0.1, FTF_CLAMP);
      AGAH D 0
      {
        A_SetShootable();
        stealth=true;
      }
      Goto See;
    EndStealth:
      AGAH BC 5 Bright A_FaceTarget();
      AGAH D 2 Bright A_StartSound("agatho/stealth");
      AGAH D 0 A_UnSetShootable();
      AGAH DDDDDDDDD 2 Bright A_FadeIn(0.1);
      AGAH D 0
      {
        A_SetShootable();
        stealth=false;
      }
      Goto See;
    Fireball:
      AGAH BC 4 A_FaceTarget();
      AGAH D 5 Bright A_SpawnProjectile("AgathoDemonFireBall", 24);
      AGAH D 0 A_Jump(70, "Tracer");
      Goto See;
    Tracer:
      AGAH BC 4 A_FaceTarget();
      AGAH D 0 A_SpawnProjectile("AgathoDemonTracer",22,0,-30,0);
      AGAH D 5 A_SpawnProjectile("AgathoDemonTracer",22,0,30,0);
      AGAH D 0 A_Jump(90, "Fireball");
      Goto See;
    Pain:
      AGAH E 3;
      AGAH E 1 A_Pain();
      AGAH F 4;
      Goto See;
    Death:
      AGAH G 8;
      AGAH H 8 A_Scream();
      AGAH I 8;
      AGAH J 8 A_SetTranslucent(1.0,0);
      AGAH K 8 A_Fall();
      AGAH L -1 A_SetFloorClip();
      Stop;
    Raise:
      AGAH L 8 A_UnSetFloorClip();
      AGAH KJIHG 8;
      Goto See;
  }
}

Class AnotherAgathodemon : Agathodemon
{
  Default
  {
    Health 500;
  }

  States
  {
  Missile:
    AGAH D 0 A_Jump(70,"Stealth");
    AGAH D 0 A_Jump(128,"Fireball");
    Goto Tracer;
  Stealth:
    AGAH D 0 A_JumpIf(stealth, "EndStealth");
    AGAH BC 5 Bright A_FaceTarget();
    AGAH D 2 Bright A_StartSound("agatho/stealth");
    AGAH D 0 A_UnSetShootable();
    AGAH DDDDDDDDD 2 Bright A_FadeOut(0.1, FTF_CLAMP);
    AGAH D 0 { stealth=true; }
    Goto See;
  EndStealth:
    AGAH BC 5 Bright A_FaceTarget();
    AGAH D 2 Bright A_StartSound("agatho/stealth");
    AGAH D 0 A_SetShootable();
    AGAH DDDDDDDDD 2 Bright A_FadeIn(0.1);
    AGAH A 0 { stealth=false; }
    Goto See;
  }
}

Class AgathoDemonFireBall : CompanionDoomImpBall //trail projectile
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Speed 20;
    Damage 9;
    Decal "DoomImpScorch";
  }

  States
  {
    Spawn:
      TNT1 A 2 Bright A_SpawnItemEx("AgathoDemonFX", 0, 0, 0, 0, 0, 0, 0, 128);
      Loop;
    Death:
      AGFX GHIJKL 3 Bright;
      Stop;
    }
}

Class AgathoDemonFX : Actor //trail effect
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Scale 0.75;
    RenderStyle "Add";
    +NOGRAVITY
  }

  States
  {
  Spawn:
    AGFX ABCDEF 2 Bright;
    Stop;
  }
}

Class AgathoDemonTracer : CompanionDoomImpBall //homing fireball
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Speed 15;
    Damage 4;
    Decal "BaronScorch";
    +SEEKERMISSILE
  }

  States
  {
  Spawn:
    AGFB ABCABCABCABCABCABCABCABCABCABCABCABC 4 Bright A_Tracer2();
  Death:
    AGFB DEFG 4 Bright;
    Stop;
  }
}
