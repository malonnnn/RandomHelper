Class Arachnophyte : Actor
{
  Default
  {
    MONSTER;
    +FRIENDLY
    Species "Companion";
    DamageFactor "Companion", 0;
    Health 2000;
    Radius 64;
    Height 80;
    Speed 12;
    PainChance 15;
    Mass 500;
    DamageFactor "Fire", 0.5;
    FloatBobStrength 0.3;
    SEESOUND "arachnophyte/sight";
    PAINSOUND "arachnophyte/pain";
    DEATHSOUND "arachnophyte/death";
    ATTACKSOUND "spider/attack";
    OBITUARY "%o was perforated by an Arachnophyte.";
    Decal "Bulletchip";
    +NoGravity
    +FloatBob
    +Float
    +NoRadiusDMG
  }

  States
  {
	Spawn:
		APYT AB 4
		{
			A_StartSound("arachnophyte/engine",CHAN_BODY,CHANF_LOOPING);
			A_Look();
		}
		Loop;
	See:
		TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
		TNT1 A 0
		{
			if (target && !random(0,4))
			{
				double ang = angleto(target);
				ThrustThing(ang*256/360+randompick(64,192),random(1,2),0,0);
				ThrustThingZ(0,random(0,5),randompick(0,1),0);
			}
		}	
		APYT AABB 2 A_Chase();
		Loop;
	Missile:
		TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
		APYT BABAB 4 A_FaceTarget();
	LoopMissile:
		TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
		APYT CD 4 A_SPosAttack();
		APYT D 1 A_SpidRefire();
		TNT1 A 0
		{
			A_FaceTarget();
			if (target && !random(0,4))
			{
				double ang = angleto(target);
				ThrustThing(ang*256/360+randompick(64,192),random(1,2),0,0);
				ThrustThingZ(0,random(0,5),randompick(0,1),0);
			}	
		}
		Loop;
	Pain:
		APYT A 4 A_Pain();
		APYT B 4;
		Goto See;
	Death:
		APYT A 4 A_StartSound("arachnophyte/death",CHAN_VOICE);
		APYT BAB 4;
		APYT E 100
		{
			A_StopSound(CHAN_BODY);
			bNOGRAVITY = false;
		}
	Crash:
		APYT F random(6,9) A_StartSound("misc/brainexplode",CHAN_BODY);
		APYT G random(6,9)
		{
			A_StartSound("misc/brainexplode",CHAN_BODY);
			A_Explode(128,255,XF_NOTMISSILE|XF_EXPLICITDAMAGETYPE, false, 0, 0, 10, "CompanionBulletPuff", "Companion");
		}
		APYT H random(6,9)
		{
			A_StartSound("misc/brainexplode",CHAN_BODY);
			A_Explode(128,128,XF_NOTMISSILE|XF_EXPLICITDAMAGETYPE, false, 0, 0, 10, "CompanionBulletPuff", "Companion"); 
		}
		APYT I 6 A_Fall();
		APYT J 6 bright;
		
		Stop;
  }
}

