Class ImpVoid : CompanionDoomImp
{
  Default
  {
    +FRIENDLY
    Species "Companion";
    DamageFactor "Companion", 0;
    Health 65;
    Radius 20;
    Height 56;
    Speed 7;
    PainChance 200;
    MONSTER;
    +FLOORCLIP
    SeeSound "imp/sight";
    PainSound "imp/pain";
    DeathSound "imp/death";
    ActiveSound "imp/active";
    Obituary "%o was burned by an imp.";
    HitObituary "%o was slashed by an imp.";
  }
  States
  {
    Spawn:
      TRO4 AB 10 A_Look;
      Loop;
    See:
      TRO4 AABBCCDD 3 A_Chase;
      Loop;
    Melee:
    Missile:
	    TRO4 EF 8 A_FaceTarget;
      TNT1 A 0 A_Jump(128,2);
      TRO4 G 6 A_CompanionTroopAttack();
      Goto See;
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      TRO4 G 6 A_CustomComboAttack("LargeImpBall",32,3,"imp/melee", "Companion");
      Goto See;
    Pain:
      TRO4 H 2;
      TRO4 H 2 A_Pain;
      Goto See;
    Death:
      TRO4 I 8;
      TRO4 J 8 A_Scream;
      TRO4 K 6;
      TRO4 L 6 A_NoBlocking;
      TRO4 M -1;
      Stop;
    XDeath:
      TROO N 5;
      TROO O 5 A_XScream;
      TROO P 5;
      TROO Q 5 A_NoBlocking;
      TROO RST 5;
      TROO U -1;
      Stop;
    Raise:
      TRO4 ML 8;
      TRO4 KJI 6;
      Goto See;
  }

  void A_CompanionTroopAttack()
	{
		let targ = target;
		if (targ)
		{
			if (CheckMeleeRange())
			{
				int damage = random[pr_troopattack](1, 8) * 3;
				A_StartSound ("imp/melee", CHAN_WEAPON);
				int newdam = targ.DamageMobj (self, self, damage, "Companion");
				targ.TraceBleed (newdam > 0 ? newdam : damage, self);
			}
			else
			{
        A_SpawnProjectile("CompanionDoomImpBall");
			}
		}
	}
}

Class LargeImpBall : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Radius 6;
    Scale 1.75;
    Height 8;
    Speed 10;
    FastSpeed 20;
    Damage 5;
    PROJECTILE;
    +RANDOMIZE
    RenderStyle "Add";
    Alpha 1;
    Decal "DoomImpScorch";
    SeeSound "imp/attack";
    DeathSound "imp/shotx";
  }
  States
  {
    Spawn:
      BAL1 AB 4 bright;
      Loop;
    Death:
      BAL1 CDE 6 bright;
      Stop;
    }
}
