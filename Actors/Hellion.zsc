Class Hellion : Actor
{
  Default
  {
    +FRIENDLY
    Species "Companion";
    DamageFactor "Companion", 0;
    Obituary "A Hellion burned %o to a crisp.";
    Health 70;
    Radius 20;
    Height 56;
    Mass 120;
    Speed 10;
    PainChance 24;
    SeeSound "monster/hlnsit";
    PainSound "monster/hlnpai";
    DeathSound "monster/hlndth";
    ActiveSound "monster/hlnact";
    Monster;
    +FloorClip
  }

  States
  {
    Spawn:
      HELN AB 10 A_Look();
      Loop;
    See:
      HELN AABBCCDDEEFF 2 A_Chase();
      Loop;
    Missile:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      HELN G 0 A_VileStart();
      HELN GHIJK 6 Bright A_FaceTarget();
      HELN L 6 Bright A_SpawnProjectile("HellionBall",32,0,0,0,0);
      HELN L 2;
      Goto See;
    Pain:
      HELN M 2;
      HELN M 2 A_Pain();
      Goto See;
    Death:
      HELN N 6;
      HELN O 6 A_Scream();
      HELN PQR 6;
      HELN S 6 A_NoBlocking();
      HELN T -1;
      Stop;
    XDeath:
      HELN U 5;
      HELN V 5 A_XScream();
      HELN W 5;
      HELN X 5 A_NoBlocking();
      HELN YZ 5;
      HEL2 AB 5;
      HEL2 C -1;
      Stop;
    Raise:
      HELN TSRQPON 6;
      Goto See;
  }
}

Class HellionBall : CompanionCacodemonBall
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Damage 8;
    Speed 15;
    Alpha 0.80;
    SeeSound "Monster/hlnatk";
    DeathSound "Monster/hlnexp";
    Decal "DoomImpScorch";
    +ThruGhost
    +ForceXYBillboard
  }

  states
  {
    Spawn:
      HLBL AB 3 Bright A_SpawnItemEx("HellionPuff", 0,0,0, 0,0,0, 0, SXF_CLIENTSIDE);
      Loop;
    Death:
      HLBL JKLMN 3 Bright;
      Stop;
  }
}

Class HellionPuff : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    +DontHarmSpecies
    -HarmFriends
    -DoHarmSpecies
    Radius 3;
    Height 3;
    RenderStyle "Add";
    Alpha 0.67;
    +NoGravity
    +NoBlockMap
    +DontSplash
    +ForceXYBillboard
  }

  States
  {
    Spawn:
      TNT1 A 3 Bright;
      HLBL CDEFGHI 3 Bright;
      Stop;
  }
}
//End of Hellion