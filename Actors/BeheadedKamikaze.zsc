class BeheadedKamikaze : Actor
{
  Default
  {
    MONSTER;
    +FRIENDLY
    Species "Companion";
    DamageFactor "Companion", 0;
    obituary "%o fell victim to a beheaded kamikazi!";
    health 20;
    radius 16;
    height 56;
    mass 100;
    speed 12;
    +FLOORCLIP
    +QUICKTORETALIATE
  }
  States
  {
    Spawn:
    Idle:
    See:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      BKAM A 0 A_StopSound(5);
      BKAM A 0 A_StartSound("kamikazi/scream", 5, 1);
      BKAM AABBCCDD 2 A_Chase;
      Loop;
    Melee:
      BKAM A 0 A_Die;
      stop;
    Death:
      BMBE M 0 bright A_SpawnItemEx ("KamikaziDamage", 0, 40);
      BMBE M 0 bright A_Stop;
      BMBE M 0 bright A_StopSound (5);
      BMBE M 5 bright A_StartSound ("weapons/rocklx");
      BMBE N 5 bright;
      BMBE O 5 A_NoBlocking;
      BMBE PQRST 5;
      BMBE U -1;
      stop;
  }
}

class KamikaziDamage : Actor
{
  Default
  {
    Radius 2;
    Height 2;
    Speed 0;
    PROJECTILE;
    Species "Companion";
    DamageType "Companion";
  }
  States
  {
    Spawn:
      TNT1 A 1;
      TNT1 A 1 A_Explode (50, 96, XF_EXPLICITDAMAGETYPE, false, 0, 0, 10, "CompanionBulletPuff", "Companion");
      stop;
  }
}
