class BeheadedKamikaze : Actor
{
  Default
  {
    Monster;
    +FRIENDLY
    Species "Companion";
    DamageFactor "Companion", 0;
    obituary "%o fell victim to a beheaded kamikazi!";
    health 20;
    radius 16;
    height 56;
    mass 100;
    speed 12;
    +FLOORCLIP
    +QUICKTORETALIATE
  }
  States
  {
    Spawn:
      BKAM A 0 A_StopSound(5);
      BKAM AB 10 A_LookEx (0, 0, 0, 1024);
      loop;
    Idle:
      BKAM A 0 A_StopSound(5);
      BKAM AB 10 A_LookEx (0, 0, 0, 1024);
      BKAM A 0 A_Jump (8, "IdleNoise");
      loop;
    IdleNoise:
      BKAM A 0 A_StartSound ("kamikazi/idle");
      goto Idle;
    See:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      BKAM A 0 A_StartSound("kamikazi/scream", 5, 1);
      BKAM A 2 A_FaceTarget;
      BKAM A 0 A_JumpIfCloser(56, "Melee");
      BKAM A 2 A_Recoil (-5);
      BKAM B 2 A_FaceTarget;
      BKAM A 0 A_JumpIfCloser(56, "Melee");
      BKAM B 2 A_Recoil (-5);
      BKAM C 2 A_FaceTarget;
      BKAM A 0 A_JumpIfCloser(56, "Melee");
      BKAM C 2 A_Recoil (-5);
      BKAM D 2 A_FaceTarget;
      BKAM A 0 A_JumpIfCloser(56, "Melee");
      BKAM D 2 A_Recoil (-5);
    CheckSee:
      BKAM D 0 A_CheckSight ("Navigate");
      goto See;
    Navigate:
      BKAM AABBCCDDAABBCCDDAABBCCDD 2 A_Chase;
      goto CheckSee;
    Melee:
      BKAM A 0 A_Die;
      stop;
    Death:
      BMBE M 0 bright A_SpawnItem ("KamikaziDamage", 0, 40);
      BMBE M 0 bright A_Stop;
      BMBE M 0 bright A_StopSound (5);
      BMBE M 5 bright A_StartSound ("weapons/rocklx");
      BMBE N 5 bright;
      BMBE O 5 A_NoBlocking;
      BMBE PQRST 5;
      BMBE U -1;
      stop;
  }
}

class KamikaziDamage : Actor
{
  Default
  {
    Radius 2;
    Height 2;
    Speed 0;
    Projectile;
    Species "Companion";
    DamageType "Companion";
  }
  States
  {
    Spawn:
      TNT1 A 1;
      TNT1 A 1 A_Explode (50, 96, 0, false, 0, 0, 10, "CompanionBulletPuff", "Companion");
      stop;
  }
}
