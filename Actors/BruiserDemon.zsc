Class BruiserDemon : Actor
{
  Default
  {
    MONSTER;
    +FRIENDLY
    Species "Companion";
    DamageFactor "Companion", 0;
    Health 1500;
    Radius 24;
    Height 64;
    Mass 2000;
    Speed 8;
    PainChance 15;
    SeeSound "superbaron/scream";
    PainSound "superbaron/pain";
    DeathSound "superbaron/death";
    ActiveSound "superbaron/act";
    Obituary "%o was slaughtered by a Bruiserdemon.";
    HitObituary "%o was cremated by a Bruiserdemon.";
    Scale 1.25;
    +Boss
    +FloorClip
    +LookAllAround
    +MissileEvenMore
    +NoRadiusDmg
  }
  States
  {
    Spawn:
      BRUS AB 10 Bright A_Look();
      Loop;
    See:
      BRUS AABBCCDD 3 Bright A_Chase();
      Loop;
    Melee:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      BRUS EF 6 Bright A_FaceTarget();
      BRUS G 6 Bright A_CustomComboAttack ("BruiserBall", 32, 20, "baron/melee", "Companion");
      Goto See;
    Missile:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      BRUS E 0 Bright A_Jump(144, "Missile2");
      BRUS EF 6 Bright A_FaceTarget();
      BRUS G 6 Bright A_SpawnProjectile("BruiserBall");
      BRUS G 0 Bright A_Jump(96, 1);
      Goto See;
      BRUS HI 6 Bright A_FaceTarget();
      BRUS J 6 Bright A_SpawnProjectile("BruiserBall");
      Goto See;
    Missile2:
      BRUS E 0 Bright A_Jump(96, "Missile3");
      BRUS E 6 Bright A_FaceTarget();
      BRUS F 4 Bright A_FaceTarget();
      BRUS F 2 Bright A_SpawnProjectile("BruiserBall2",56,0,-40,0);
      BRUS G 1 Bright A_SpawnProjectile("BruiserBall2",56,0,-30,0);
      BRUS G 1 Bright A_SpawnProjectile("BruiserBall2",56,0,-20,0);
      BRUS G 1 Bright A_SpawnProjectile("BruiserBall2",56,0,-10,0);
      BRUS G 1 Bright A_SpawnProjectile("BruiserBall2",56,0,  0,0);
      BRUS G 1 Bright A_SpawnProjectile("BruiserBall2",56,0, 10,0);
      BRUS G 1 Bright A_SpawnProjectile("BruiserBall2",56,0, 20,0);
      BRUS G 0 Bright A_Jump(96, 1);
      Goto See;
      BRUS H 6 Bright A_FaceTarget();
      BRUS I 4 Bright A_FaceTarget();
      BRUS I 2 Bright A_SpawnProjectile("BruiserBall2",56,0, 40,0);
      BRUS J 1 Bright A_SpawnProjectile("BruiserBall2",56,0, 30,0);
      BRUS J 1 Bright A_SpawnProjectile("BruiserBall2",56,0, 20,0);
      BRUS J 1 Bright A_SpawnProjectile("BruiserBall2",56,0, 10,0);
      BRUS J 1 Bright A_SpawnProjectile("BruiserBall2",56,0,  0,0);
      BRUS J 1 Bright A_SpawnProjectile("BruiserBall2",56,0,-10,0);
      BRUS J 1 Bright A_SpawnProjectile("BruiserBall2",56,0,-20,0);
      Goto See;
    Missile3:
      BRUS KL 6 Bright A_FaceTarget();
      BRUS M 2 Bright A_SpawnProjectile("BruiserFireSpawner",16,0,-15,0);
      BRUS M 2 Bright A_SpawnProjectile("BruiserFireSpawner",16,0,  0,0);
      BRUS M 2 Bright A_SpawnProjectile("BruiserFireSpawner",16,0, 15,0);
      Goto See;
    Pain:
      BRUS N 5 Bright A_Pain();
      Goto See;
    Death:
      BRUD A 6 Bright A_Scream();
      BRUD BCDEFG 4 Bright;
      BRUD H 4 Bright A_Fall();
      BRUD IJKLMNOP 4 Bright;
      BRUD QRSTUV 4;
      BRUD W -1;
      Stop;
  }
}

Class BruiserBall : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Projectile;
    Radius 16;
    Height 16;
    Speed 18;
    Damage 16;
    RenderStyle "Add";
    Alpha 0.9;
    SeeSound "imp/attack";
    DeathSound "imp/shotx";
    Decal "Scorch";
    +Randomize;
  }

  States
  {
    Spawn:
      BRBA AABB 2 Bright A_SpawnItemEx("BruiserBallTrail");
      Loop;
    Death:
      BRBA KLMNOPQRSTUVWX 3 Bright;
      Stop;
  }
}

Class BruiserBall2 : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Projectile;
    Radius 8;
    Height 8;
    Speed 10;
    Damage 8;
    RenderStyle "Add";
    Alpha 0.9;
    SeeSound "imp/attack";
    DeathSound "imp/shotx";
    Decal "BaronScorch";
    +Randomize
  }

  States
  {
    Spawn:
      BRB2 AB 6 Bright;
      Loop;
    Death:
      BRB2 CDEFGHI 3 Bright;
      Stop;
  }
}

Class BruiserFireSpawner : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Projectile;
    Radius 8;
    Height 8;
    Speed 12;
    Damage 0;
    +Ripper
    +FloorHugger
    +BloodlessImpact
  }

  States
  {
    Spawn:
      TNT1 A 3 Bright A_SpawnProjectile("BruiserFire", 0, 0, 0, CMF_TRACKOWNER);
      TNT1 AA 3;
      Loop;
    Death:
      TNT1 A 6;
      Stop;
  }
}

Class BruiserBallTrail : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Projectile;
    Radius 1;
    Height 1;
    Speed 0;
    RenderStyle "Add";
    Alpha 0.70;
    +NoClip
  }

  States
  {
    Spawn:
      BRBA CDEFGHIJ 4 Bright;
      Stop;
  }
}

Class BruiserFire : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Radius 1;
    Height 1;
    Speed 0;
    Damage 20;
    RenderStyle "Add";
    Alpha 0.9;
    SeeSound "weapons/rocklx";
    Projectile;
    +NoClip
    +FloorHugger
  }

  States
  {
    Spawn:
      XXBF AB 3 Bright;
      XXBF C 3 Bright A_Explode(48,128,XF_EXPLICITDAMAGETYPE, false, 0, 0, 10, "CompanionBulletPuff", "Companion");
      XXBF DEFGHIJKLMNOPQRST 3 Bright;
      Stop;
  }
}
