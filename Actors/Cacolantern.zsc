Class Cacolantern : Actor
{
  Default
  {
    MONSTER;
    +FRIENDLY
    Species "Companion";
    DamageFactor "Companion", 0;
    Health 800;
    Speed 8;
    Radius 31;
    Height 56;
    PainChance 128;
    Mass 400;
    REACTIONTIME 8;
    BloodColor "EB 6F 0F";
    +NOGRAVITY
    +FLOAT
    SeeSound "caco/sight";
    PainSound "caco/pain";
    DeathSound "caco/death";
    ActiveSound "caco/active";
    HitObituary "%o admired the Cacolantern's teeth.";
    Obituary "%o couldn't take %h eyes off the eye.";
  }
  States
  {
    Spawn:
      HED2 A 10 bright A_Look;
      Loop;
    See:
      HED2 A 3 bright A_Chase;
      Loop;
    Melee:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      HED2 BC 5 bright A_FaceTarget;
      HED2 D 5 bright A_CustomMeleeAttack (Random(1, 8) * 6, "", "", "Companion");
      Goto See;
    Missile:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      HED2 BC 5 bright A_FaceTarget;
      HED2 D 5 bright A_CustomComboAttack ("CacolanternBall", 30, 9, "", "Companion");
      Goto See;
    Pain:
      HED2 E 4 bright;
      HED2 E 4 bright A_Pain;
      HED2 F 5 bright;
      Goto See;
    Death:
      HED2 G 8 bright;
      HED2 H 8 bright A_Scream;
      HED2 I 8 bright;
      HED2 J 8;
      HED2 K 8 A_NoBlocking;
      HED2 L -1 A_SetFloorClip;
      Stop;
    Raise:
      HED2 L 8 A_UnsetFloorClip;
      HED2 KJ 8;
      HED2 IHG 8 bright;
      Goto See;
  }
}

Class CacolanternBall : Actor
{
  Default
  {
    PROJECTILE;
    Species "Companion";
    DamageType "Companion";
    Radius 13;
    Height 8;
    Speed 20;
    Damage 9;
    RENDERSTYLE "ADD";
    ALPHA 0.67;
    SeeSound "imp/attack";
    DeathSound "imp/shotx";
    Decal "CacoScorch";
  }
  States
  {
    Spawn:
      BAL8 ABCDEFGH 5 Bright;
      Loop;
    Death:
      BAL8 IJKL 4 Bright;
      Stop;
  }
}

