Class HellsFury : Actor
{
  Default
  {
    MONSTER;
    +FRIENDLY
    Species "Companion";
    DamageFactor "Companion", 0;
    Health 1000;
    Radius 24;
    Height 64;
    Speed 8;
    PainChance 50;
    Mass 1000;
    SeeSound "baron/sight";
    ActiveSound "baron/active";
    PainSound "baron/pain";
    DeathSound "baron/death";
    Obituary "%o felt the wrath of Hell's Fury";
    HitObituary "%o got ripped apart by Hell's Fury";
    +FLOORCLIP
  }

  States
  {
    Spawn:
      HFRY AB 10 A_Look();
      Loop;
    See:
      HFRY AABBCCDD 4 A_Chase();
      Loop;
    Missile:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      HFRY E 0 A_Jump(96, "Missile2");
      HFRY EF 8 A_FaceTarget();
      //HFRY G 8 A_BasicAttack(11, "baron/melee", "HellsFuryBall", 32);
      HFRY G 8 A_CustomComboAttack("HellsFuryBall", 32, 11, "Baron/Melee");
      Goto See;
    Missile2:
      HFRY P 8 A_FaceTarget();
      HFRY Q 8
      {
        A_SpawnProjectile("SoulHarvesterBall", 60, 0, -45);
        A_SpawnProjectile("SoulHarvesterBall", 60, 0,   0);
        A_SpawnProjectile("SoulHarvesterBall", 60, 0,  45);
      }
      Goto See;
    Melee:
      TNT1 A 0 A_AlertMonsters(0, AMF_TARGETEMITTER);
      HFRY EF 8 A_FaceTarget();
      HFRY G 8 A_CustomComboAttack("HellsFuryBall", 32, 11, "Baron/Melee");
      Goto See;
    Pain:
      HFRY H 2;
      HFRY H 2 A_Pain();
      Goto See;
    Death:
      HFRY I 8 A_Scream();
      HFRY J 0 A_FaceTarget();
      HFRY J 8 A_SpawnItemEx("HellsFurySkull", 0, 0, 60, 5, 0, 0, RandomPick(20, 10, 0, -10, -20), SXF_CLIENTSIDE);
      HFRY K 8;
      HFRY L 8 A_Fall();
      HFRY MN 8;
      HFRY O -1;
      Stop;
    Raise:
      HFRY NMLKJI 8;
      Goto See;
   }
}

Class HellsFuryBall : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Radius 6;
    Height 8;
    Speed 14;
    Damage 8;
    RenderStyle "Add";
    Alpha 0.7;
    SeeSound "imp/attack";
    DeathSound "imp/shotx";
    Decal "BaronScorch";
    Projectile;
  }

  States
  {
    Spawn:
      HFBB AB 5 Bright;
      Loop;
    Death:
      HFBB CDE 5 Bright;
      Stop;
  }
}

Class SoulHarvesterBall : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Radius 8;
    Height 6;
    Scale 0.4;
    Speed 9;
    Damage 2;
    RenderStyle "Add";
    SeeSound "harvester/scream";
    DeathSound "imp/shotx";
    Decal "DoomImpScorch";
    Projectile;
    +SeekerMissile
  }

  States
  {
    Spawn:
      SHBA A 0 Bright A_SpawnItemEx("SoulBallTrail", 0, 0, 0, 0, 0, 0, 180, SXF_CLIENTSIDE);
      SHBA AB 2 Bright A_SeekerMissile(10,20);
      Loop;
    Death:
      SHBA CDEFGHIJKL 4 Bright;
      Stop;
  }
}

Class HellsFurySkull : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Radius 5;
    Height 5;
    Speed 5;
    Mass 5000;
    +DropOff
    +DoomBounce
    +Missile
  }

  States
  {
    Spawn:
      FSKL ABCDEFGHIJK 2;
      Loop;
    Death:
      FSKL L -1;
      Loop;
  }
}

Class SoulBallTrail : Actor
{
  Default
  {
    Species "Companion";
    DamageType "Companion";
    Radius 1;
    Height 1;
    Speed 0;
    RenderStyle "Add";
    Alpha 0.5;
    Projectile;
    +NoClip
  }

  States
  {
    Spawn:
    Death:
      SBTR H 6;
      SBTR ABCDEFG 4 Bright;
      Stop;
  }
}

