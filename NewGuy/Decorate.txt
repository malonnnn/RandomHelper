ACTOR Clusterfiend
{
	Health 225
	Radius 31
	Height 56
	Mass 600
	Speed 8
	PainChance 64
	Monster
	+FLOAT
	+NOGRAVITY
	+NORADIUSDMG
	SeeSound "clust/sight"
	DeathSound "clust/death"
	PainSound "caco/pain"
	ActiveSound "caco/active"
	Obituary "%o couldn't dodge a Clusterfiend."
	HitObituary "%o got munched by a Clusterfiend."
	States
	{
	Spawn:
		CEAD A 10 A_Look
		Loop
	See:
		CEAD A 3 A_Chase
		Loop
	Missile:
		CEAD BC 5 A_FaceTarget
		CEAD D 3 Bright A_CustomMissile("SingleSplitBall")
		CEAD D 2 Bright A_CustomMissile("ButtPlasma", 4,0, 180,CMF_AIMDIRECTION,0)
		Goto See
	Pain:
		CEAD E 3
		CEAD E 3 A_Pain
		CEAD F 6
		Goto See
	Death:
		CEAD G 8
		CEAD H 8 A_Scream
		CEAD I 8
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_SpawnItemEx("HealthBonus",1,0,32, 1,  1, 9 ,0)
		TNT1 A 0 A_SpawnItemEx("HealthBonus",1,0,32, 1, -2,11 ,0)
		TNT1 A 0 A_SpawnItemEx("HealthBonus",1,0,32, 1,-.5,18 ,0)
		CEAD I 0 A_SpawnItemEx("HealthBonus",1,0,32, 1, .5, 6 ,0,0,80)
		CEAD I 0 A_SpawnItemEx("HealthBonus",1,0,32, 1, -1, 8 ,0,0,80)
		CEAD J 8	
		CEAD K 8 A_NoBlocking
		CEAD L -1 A_SetFloorClip
		Stop
	Raise:
		CEAD L 8 A_UnSetFloorClip
		CEAD KJIHG 8
		Goto See
	}
}

ACTOR Midasfiend : Clusterfiend
{
	Health 450
	Mass 900
	SeeSound "clust/sight"
	DeathSound "clust/death"
	PainSound "caco/pain"
	ActiveSound "caco/active"
	Obituary "%o couldn't dodge a Midasfiend."
	HitObituary "%o received the Midasfiend's touch."
	States
	{
		Spawn:
		GEAD A 10 A_Look
		Loop
	See:
		GEAD A 3 A_Chase
		Loop
	Missile:
		GEAD BC 5 A_FaceTarget
		GEAD D 3 Bright A_CustomMissile("QuadrupleSplitBall")
		GEAD D 2 Bright A_CustomMissile("ButtPlasma",4,0, 180,CMF_AIMDIRECTION,0)
		Goto See
	Pain:
		GEAD E 3
		GEAD E 3 A_Pain
		GEAD F 6
		Goto See
	Death:
		GEAD G 8
		GEAD H 8 A_Scream
		GEAD I 8
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_SpawnItemEx("ArmorBonus",1,0,32, 1,  1, 9 ,0)
		TNT1 A 0 A_SpawnItemEx("ArmorBonus",1,0,32, 1, -2,11 ,0)
		TNT1 A 0 A_SpawnItemEx("ArmorBonus",1,0,32, 1,-.5,18 ,0)
		TNT1 A 0 A_SpawnItemEx("ArmorBonus",1,0,32, 1, .5, 6 ,0,0,80)
		TNT1 A 0 A_SpawnItemEx("ArmorBonus",1,0,32, 1, -1, 8 ,0,0,80)
		GEAD J 8	
		GEAD K 8 A_NoBlocking
		GEAD L -1 A_SetFloorClip
		Stop
	Raise:
		GEAD L 8 A_UnSetFloorClip
		GEAD KJIHG 8
		Goto See
	}
}

ACTOR ButtPlasma : ArachnotronPlasma
{
	SeeSound ""
	Damage 7
	var int user_temp;
	var int user_dingleTimer;
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetUserVar("user_dingleTimer",22)//num of frames
		TNT1 A 0 A_Warp(AAPTR_TARGET,-23,0,4,0)//warps to butt position
		goto Dangle
	Dangle:
		TNT1 A 0 A_JumpIf(user_dingleTimer == 0,"ReadjustTrajectory")
		TNT1 A 0 A_SetUserVar("user_temp",user_dingleTimer)
		TNT1 A 0 A_SetUserVar("user_dingleTimer",user_temp - 1)
		TNT1 A 0 A_Warp(AAPTR_TARGET,-23,0,4,0)
		SPLT A 1 Bright
		TNT1 A 0 A_JumpIf(user_dingleTimer == 0,"ReadjustTrajectory")
		TNT1 A 0 A_SetUserVar("user_temp",user_dingleTimer)
		TNT1 A 0 A_SetUserVar("user_dingleTimer",user_temp - 1)
		TNT1 A 0 A_Warp(AAPTR_TARGET,-23,0,4,0)
		SPLT B 1 Bright
		goto Dangle
	ReadjustTrajectory:
		SPLT B 0 Bright A_ChangeVelocity(0,0,0,CVF_RELATIVE|CVF_REPLACE)
		goto PostDeathAnimation
	Death:
		TNT1 A 0
		SPLX BC 4 Bright
	PostDeathAnimation:
		SPLX DE 4 Bright
		Stop
	}
}

ACTOR SingleSplitBall : CacodemonBall
{
	Damage 10
	Speed 15
	SeeSound "clust/fire"
	DeathSound ""
	Translation "224:231=112:117", "160:167=118:123", "70:79=121:121", "168:173=160:165", "16:31=227:231"
	BounceType Doom
	BounceFactor 1.0
	WallBounceFactor 1.0
	BounceCount 2
	+USEBOUNCESTATE
	var int user_surface;
    //0 = actor
	//1 = wall
	//2 = floor
	//3 = ceiling
	var int user_pitch;//used to store the vertical angle of impact
	var int user_explode;//1 = splash damage ; 0 = no splash damage
	States
	{
	Spawn:
		CAL2 AB 4 Bright
		TNT1 A 0 A_SetUserVar("user_surface",0)
		TNT1 A 0 A_SetUserVar("user_explode",0)
		Loop
	Bounce.Wall:
		TNT1 A 0 A_SetUserVar("user_surface",1)
		TNT1 A 0 A_SetUserVar("user_explode",1)
		goto Death
	Bounce.Floor:
		TNT1 A 0 A_SetUserVar("user_surface",2)
		TNT1 A 0 A_SetUserVar("user_explode",1)
		goto Death
	Bounce.Ceiling:
		TNT1 A 0 A_SetUserVar("user_surface",3)
		TNT1 A 0 A_SetUserVar("user_explode",1)
		goto Death
	Death:
		TNT1 A 0 A_JumpIf(sqrt((velX*velX)+(velY*velY))==0.0,"VerticalAngle")
		TNT1 A 0 A_SetUserVar("user_pitch",(180.0/3.14159)*((velZ/sqrt((velX*velX)+(velY*velY)))-(((velZ/sqrt((velX*velX)+(velY*velY)))*(velZ/sqrt((velX*velX)+(velY*velY)))*(velZ/sqrt((velX*velX)+(velY*velY))))/3)+(((velZ/sqrt((velX*velX)+(velY*velY)))*(velZ/sqrt((velX*velX)+(velY*velY)))*(velZ/sqrt((velX*velX)+(velY*velY)))*(velZ/sqrt((velX*velX)+(velY*velY)))*(velZ/sqrt((velX*velX)+(velY*velY))))/5)))
		goto DoneCalculatingPitch
	VerticalAngle://go here to prevent division by zero
		TNT1 A 0 A_SetUserVar("user_pitch",90)
		goto DoneCalculatingPitch
	DoneCalculatingPitch:
		TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE)
		TNT1 A 0 A_JumpIf(user_surface == 0, "FinishDieing")
		TNT1 A 0 A_CustomMissile("SmallerPlasma",0,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER,user_pitch/3)//shallow pitch
	FinishDieing:
		TNT1 A 0 A_PlaySound("clust/split1")
		TNT1 A 0 A_JumpIf(user_explode == 0, "MiniExplosion")
		TNT1 A 0 A_Explode(64,70,0)
		goto WrapThingsUp
	MiniExplosion:
		CAL2 C 6 Bright A_Explode(8,70,0)
		goto WrapThingsUp
	WrapThingsUp:
		CAL2 DE 6 Bright
		Stop
	}
}

ACTOR QuadrupleSplitBall : CacodemonBall
{
	Damage 10
	Speed 20
	SeeSound "clust/fire"
	DeathSound "clust/split4"
	States
	{
	Spawn:
		CAL2 AB 4 Bright
		Loop
	Death:
		TNT1 A 0 A_SetAngle(angle+45)
		TNT1 A 0 A_CustomMissile("SmallerPlasma",0,4,-90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
		TNT1 A 0 A_SetAngle(angle+90)
		TNT1 A 0 A_CustomMissile("SmallerPlasma",0,4,-90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
		TNT1 A 0 A_SetAngle(angle+90)
		TNT1 A 0 A_CustomMissile("SmallerPlasma",0,4,-90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
		TNT1 A 0 A_SetAngle(angle+90)
		TNT1 A 0 A_CustomMissile("SmallerPlasma",0,4,-90,CMF_AIMDIRECTION|CMF_TRACKOWNER)
		TNT1 A 0 A_SetAngle(angle+45)
		CAL2 CDE 6
		Stop
	}
}

ACTOR SmallerPlasma : ArachnotronPlasma
{
	SeeSound ""
	Speed 15
	Damage 6
	Radius 2
	States
	{
	Spawn:
		SPLT AB 5 Bright
		Loop
	Death:
		SPLX BCDE 5 Bright
		Stop
	}
}